package plaid.examples.sharedArrayTest;

import plaid.arrays.SharedArray;
import plaid.arrays.AbstractSharedArrayOperations;
import plaid.arrays.makeSharedArray;

/***************** data object *******************/

state Data case of Object {
    val immutable Integer value;
}

method unique Data makeData(immutable Integer value) {
    new Data { val immutable Integer value = value; }
}


/***************** ops object *******************/

state MySharedArrayOperations case of AbstractSharedArrayOperations {
    method shared<owner> Object initialize<group exclusive owner>(immutable Integer index) [ immutable MySharedArrayOperations this ] {
        printLine("initialize " + index.toString());
        makeData(index);
    } 

    override method void doExclusiveData1<group exclusive owner>(shared<owner> ?Object obj, immutable Object data) [ immutable MySharedArrayOperations this ] {
        printLine("doExclusive");
        match (obj) {
            case Data {
                printLine("Element " );
            }
            default { 
                printLine("Empty Slot");
            }
        }
    }
    
    override method void doSharedData1<group shared owner>(shared<owner> ?Object obj, immutable Object data) [ immutable MySharedArrayOperations this ] {
        printLine("doShared");
        match (obj) {
            case Data {
                printLine("Element " );
            }
            default { 
                printLine("Empty Slot");
            }
        }
    }
}

/***************** main *******************/

method void main() {
    val unique SharedArray sa = makeSharedArray(6);
    val immutable MySharedArrayOperations ops = new MySharedArrayOperations;
    val immutable Data data = makeData(0);
    val unique Data data1 = makeData(1);
    val unique Data data2 = makeData(2);
    val unique Data data3 = makeData(3);
    
    //****** unique receiver
    sa.doUniqueData1(1, ops, data);
    sa.setUnique(1, data1);
    sa.doUniqueData1(1, ops, data);
    
    //****** shared receiver
    val shared SharedArray sa2 = sa;
    sa2.doSharedData1(2, ops, data);
    sa2.setShared(2, data2);
    sa2.doSharedData1(2, ops, data);
    
    //****** local shared receiver
    val local shared SharedArray sa3 = sa2;
    sa3.doLocalSharedData1(3, ops, data);
    sa3.setLocalShared(3, data3);
    sa3.doLocalSharedData1(3, ops, data);
    
    //******* initialize
    val unique SharedArray saInit = makeSharedArray(6);
    saInit.initialize(ops);
}