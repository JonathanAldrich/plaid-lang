package plaid.typechecker;

import plaid.ast.visitor.LeafVisitor;

/**
 *	state which outputs typechecker information in a nicely printed manor (eventually)
 */
state TypecheckOutputVisitor case of LeafVisitor {
	
	val immutable String prefix = "TYPE ERROR: ";

	method errorString(msg, tokenOpt) {
		match (tokenOpt) {
			case Some {
				prefix + tokenOpt.value.errorOutput() + " " + msg;
			}
			case None {
				prefix + msg;
			}
		}
	}
	
	method checkError(node) {
		match (node.type) {
			case ErrorType {
				if (node.type.isLocal) { //report
					java.lang.System.out.println(this.errorString(node.type.errorMsg, node.token));
				}
			}
			default { }
		}	
	}

	override method visitMethodCall(node) {
		checkError(node);
		node.receiver.accept(this);
		node.argument.accept(this);
	}
	
	override method visitApplication(node) {
		checkError(node);
		node.f.accept(this);
		node.arg.accept(this);
	}
	
	override method visitDereference(node) {
		checkError(node);
		node.left.accept(this);
	}
	
	override method visitDestructiveDereference(node) {
		checkError(node);
		node.left.accept(this);
	}
	
	override method visitMatch(node) {
		checkError(node);
		node.e.accept(this);
		node.caseList.map(fn (c) => c.accept(this));
	}
	
	override method visitCase(node) {
		checkError(node);
		node.e.accept(this);
	}
	
	override method visitID(node) {
		checkError(node);
	}
	
	override method visitLambda(node) {
		checkError(node);
		node.body.accept(this);
	}
	
	override method visitNewInstance(node) {
		checkError(node);
		match(node.s.initState) {
			case Some {
				node.s.initState.value.accept(this);
			}
			case None {
			
			}
		}
	}
	
	
}