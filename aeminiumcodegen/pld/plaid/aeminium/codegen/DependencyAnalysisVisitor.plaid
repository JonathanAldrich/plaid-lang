package plaid.aeminium.codegen;

import plaid.ast.*;
import plaid.ast.types.*;
import plaid.ast.visitor.*;

state DependencyAnalysisVisitor = ASTVisitor with GraphOutput with SplitJoinReduction with {
	var writer = java.util.HashMap.new();
	var readers = java.util.HashMap.new();
	var deps = java.util.HashSet.new();
	var nodeList = new List { var head = new Nil; };
	
	val methodTypes;
	
	var separateTask = true;
	var savedWrites;
	var savedReads;
	
	var counter = 0;
	
	method getNodeNumber() {
		ifElse (this.separateTask) {
			val c = this.counter;
			this.counter = this.counter + 1;
			c;
		} {
			-1;
		};
	}

	method makeDependency(f, t, v) {
		new Dependency {
			val from = f;
			val to = t;
			val variable = v;
		};
	}
	
	method updateDependencies(astNode, reads, writes) {
		this.savedReads = reads;
		this.savedWrites = writes;
		
		if (this.separateTask) {
			val myDeps = java.util.HashSet.new();
			this.nodeList.add(astNode);
			
			var it = reads.iterator();
			while { it.hasNext(); } {
				val read = it.next();
				
				if (writer.containsKey(read.name)) {
					val prevWriter = writer.get(read.name);
					
					myDeps.add(makeDependency(astNode, prevWriter, read));
				};
				
				if (!readers.containsKey(read.name)) {
					readers.put(read.name, java.util.HashSet.new());
				};
				
				readers.get(read.name).add(astNode);
			};
			
			it = writes.iterator();
			while { it.hasNext(); } {
				val write = it.next();
				
				ifElse (readers.containsKey(write.name)) {
					val prevReaders = readers.get(write.name);
					val readIt = prevReaders.iterator();
					
					while { readIt.hasNext(); } {
						val prevReader = readIt.next();
						// TODO: Add inequality check.
						myDeps.add(makeDependency(astNode, prevReader, write));
					};
				} {
					if (writer.containsKey(write.name)) {
						val prevWriter = writer.get(write.name);
						// TODO: Add inequality check.
						myDeps.add(makeDependency(astNode, prevWriter, write));
					};
				};
				
				writer.put(write.name, astNode);
				readers.remove(write.name);
			};
			
			this.deps.addAll(myDeps);
			astNode.dependencies = new Some { val value = myDeps; };
			
			this.reduceDependencies(astNode);
		};
	}
	
	method visitCompilationUnit(node) {
		node.decls.map(fn (decl) => {
			decl.accept(this)
		});
	}
	
	
	method visitMethodDecl(astNode) {
		java.lang.System.out.println("Visiting " + astNode.name + ".");
		this.counter = 0;
		astNode.num = getNodeNumber();
		this.writer = java.util.HashMap.new();
		this.readers = java.util.HashMap.new();
		this.deps = java.util.HashSet.new();
		this.nodeList = new List { var head = new Nil; };


		val reads = java.util.HashSet.new();
		val writes = java.util.HashSet.new();
		
		match (astNode.arg) {
			case Some {
				writes.add(astNode.arg.value);
			}
			case None {}
		};
		
		updateDependencies(astNode, reads, writes);
		astNode.body.accept(this);
		
		printDependencies();
		createDependencyGraph();
	}
	
	method visitSplit(astNode) {
		astNode.num = getNodeNumber();
		val reads = java.util.HashSet.new();
		val writes = java.util.HashSet.new();
		
		// We consider a split a write iff the permission of the split variable is changed.
		val inPerm = astNode.in.type.value.perm;
		val outPerm = astNode.out1.type.value.perm;
		
		ifElse (inPerm == outPerm) {
			reads.add(astNode.in);
		} {
			writes.add(astNode.in);
		};
		
		updateDependencies(astNode, reads, writes);
		astNode.body.accept(this);
	}
	
	method visitJoin(astNode) {
		astNode.num = getNodeNumber();
		val reads = java.util.HashSet.new();
		val writes = java.util.HashSet.new();
		
		// We consider a join a write to a variable v iff:
		// 1) v goes out of scope (our equivalent of leaving v with a none permission).
		//    This happens if in1 and in2 do not refer to the same variable.
		// 2) The permission of v changes.
		//
		// Otherwise we consider the join a reader of variable v.
		
		val in1Perm = astNode.in1.type.value.perm;
		val in2Perm = astNode.in2.type.value.perm;
		val outPerm = astNode.out.type.value.perm;
		
		// Check if a variable goes out of scope.
		ifElse (astNode.in1.name.equals(astNode.in2.name) && astNode.in2.name.equals(astNode.out.name)) {
			// No variable goes out of scope.
			ifElse ((in1Perm == in2Perm) && (in2Perm == outPerm)) {
				// v's permission does not change.
				reads.add(astNode.in1);
			} {
				// v's permission does change.
				writes.add(astNode.in1);
			};
		} {
			// Either in1 or in2 goes out of scope.
			ifElse (astNode.in1.name.equals(astNode.out.name)) {
				// in2 goes out of scope.
				writes.add(astNode.in2);
				
				// Check if in1's permission changes.
				ifElse (in1Perm == outPerm) {
					reads.add(astNode.in1);
				} {
					writes.add(astNode.in1);
				};
			} {
				// in1 goes out of scope.
				writes.add(astNode.in1);
				
				// Check if in2's permission changes.
				ifElse (in2Perm == outPerm) {
					reads.add(astNode.in2);
				} {
					writes.add(astNode.in2);
				};
			};
		};
		
		updateDependencies(astNode, reads, writes);
		astNode.body.accept(this);
	}
	
	method visitLetBinding(astNode) {
		astNode.num = getNodeNumber();
		val reads = java.util.HashSet.new();
		val writes = java.util.HashSet.new();
		
		writes.add(astNode.x);
		
		// TODO: Solve this in a clean way.
		// The problem is that we view the "Let x = E1" part of "Let x = E1 in E2"
		// as belonging together, i.e. we don't want the AST node of E1 to depend
		// on some other node N but we want the let AST node itself to depend on N. 
		this.separateTask = false;
		astNode.exp.accept(this);
		this.separateTask = true;
		
		reads.addAll(this.savedReads);
		writes.addAll(this.savedWrites);
		
		updateDependencies(astNode, reads, writes);
		astNode.body.accept(this);		
	}
	
	method visitApplication(astNode) {
		astNode.num = getNodeNumber();
		val reads = java.util.HashSet.new();
		val writes = java.util.HashSet.new();
		
		// TODO: How to handle methods with multiple arguments?
		// arg could be an ID or a literal.  We only care about the IDs.
		match (astNode.arg) {
			case ID {
				// Look up the method type in question.
				java.lang.System.out.println("Looking up " + astNode.f.name);
				val methodType = this.methodTypes.get(astNode.f.name);
				val argTypes = methodType.argTypes;
				java.lang.System.out.println("Matching.");
				match (argTypes.head) {
					case Cons {
						val argType = argTypes.head.value;
						
						match (argType.perm) {
							case UniquePermission {
								writes.add(astNode.arg);
							}
							
							case ImmutablePermission {
								reads.add(astNode.arg);
							}
						}
					}
				}
			}
			default {}
		};
		
		updateDependencies(astNode, reads, writes);
	}
	
	method visitID(astNode) {
		astNode.num = getNodeNumber();
		val reads = java.util.HashSet.new();
		
		reads.add(astNode);

		updateDependencies(astNode, reads, java.util.HashSet.new());
	}
	
	method visitUnitLiteral(astNode) {
		astNode.num = getNodeNumber();
		
		updateDependencies(astNode, java.util.HashSet.new(), java.util.HashSet.new());
	}
}