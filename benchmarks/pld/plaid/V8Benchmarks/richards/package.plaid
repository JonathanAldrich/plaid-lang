package plaid.V8Benchmarks.richards;

import plaid.V8Benchmarks.if;
import plaid.V8Benchmarks.ifElse;
import plaid.collections.makeArray;

/**
 * The task is running and is currently scheduled.
 */
val STATE_RUNNING = 0;

/**
 * The task has packets left to process.
 */
val STATE_RUNNABLE = 1;

/**
 * The task is not currently running.  The task is not blocked as such and may
* be started by the scheduler.
 */
val STATE_SUSPENDED = 2;

/**
 * The task is blocked and cannot be run until it is explicitly released.
 */
val STATE_HELD = 4;

val STATE_SUSPENDED_RUNNABLE = 6; // STATE_SUSPENDED | STATE_RUNNABLE
val STATE_NOT_HELD = -5; //STATE_HELD

val NEWNONE = new None;

val COUNT = 1000;

/**
 * These two constants specify how many times a packet is queued and
 * how many times a task is put on hold in a correct run of richards.
 * They don't have any meaning a such but are characteristic of a
 * correct run so if the actual queue or hold count is different from
 * the expected there must be a bug in the implementation.
 **/
val EXPECTED_QUEUE_COUNT = 2322;
val EXPECTED_HOLD_COUNT = 928;


val ID_IDLE       = 0;
val ID_WORKER     = 1;
val ID_HANDLER_A  = 2;
val ID_HANDLER_B  = 3;
val ID_DEVICE_A   = 4;
val ID_DEVICE_B   = 5;
val NUMBER_OF_IDS = 6;

val KIND_DEVICE   = 0;
val KIND_WORK     = 1;


/**
 * The Richards benchmark simulates the task dispatcher of an
 * operating system.
 **/
method runRichardsCompiled() {
  
  var scheduler = new Scheduler;
  scheduler.addIdleTask(ID_IDLE, 0, NEWNONE, COUNT);

  var queue = new Packet;
  queue.setData(NEWNONE, ID_WORKER, KIND_WORK);
  var queue2 = new Packet;
  queue2.setData(queue,  ID_WORKER, KIND_WORK);
  scheduler.addWorkerTask(ID_WORKER, 1000, queue2);

  var queue3 = new Packet;
  queue3.setData(NEWNONE, ID_DEVICE_A, KIND_DEVICE);
  var queue4 = new Packet;
  queue4.setData(queue3,  ID_DEVICE_A, KIND_DEVICE);
  var queue5 = new Packet;
  queue5.setData(queue4,  ID_DEVICE_A, KIND_DEVICE);
  scheduler.addHandlerTask(ID_HANDLER_A, 2000, queue5);

  var queue6 = new Packet;
  queue6.setData(NEWNONE, ID_DEVICE_B, KIND_DEVICE);
  var queue7 = new Packet;
  queue7.setData(queue6,  ID_DEVICE_B, KIND_DEVICE);
  var queue8 = new Packet;
  queue8.setData(queue7,  ID_DEVICE_B, KIND_DEVICE);
  scheduler.addHandlerTask(ID_HANDLER_B, 3000, queue8);

  scheduler.addDeviceTask(ID_DEVICE_A, 4000, NEWNONE);

  scheduler.addDeviceTask(ID_DEVICE_B, 5000, NEWNONE);

  scheduler.schedule();

  if (scheduler.queueCount != EXPECTED_QUEUE_COUNT ||
      scheduler.holdCount != EXPECTED_HOLD_COUNT) {
    var msg =
        "Error during execution: queueCount = " + scheduler.queueCount.toString() +
        ", holdCount = " + scheduler.holdCount.toString() + ".";
    printLine(msg);
  }
}

/**
 * A scheduler can be used to schedule a set of tasks based on their relative
 * priorities.  Scheduling is done by maintaining a list of task control blocks
 * which holds tasks and the data queue they are processing.
 * @constructor
 */
state Scheduler {
  var queueCount = 0;
  var holdCount = 0;
  var blocks = makeArray(NUMBER_OF_IDS);
  var list = NEWNONE;
  var currentTcb = NEWNONE;
  var currentId = NEWNONE;

/**
 * Add an idle task to this scheduler.
 * @param {int} id the identity of the task
 * @param {int} priority the task's priority
 * @param {Packet} queue the queue of work to be processed by the task
 * @param {int} count the number of times to schedule the task
 */
method addIdleTask(id, priority, queue, count) {
  var it = new IdleTask;
  it.setData(this,1,count);
  this.addRunningTask(id, priority, queue, it);
}

/**
 * Add a work task to this scheduler.
 * @param {int} id the identity of the task
 * @param {int} priority the task's priority
 * @param {Packet} queue the queue of work to be processed by the task
 */
method addWorkerTask(id, priority, queue) {
  var wt = new WorkerTask;
  wt.setData(this, ID_HANDLER_A, 0);
  this.addTask(id, priority, queue, wt);
}

/**
 * Add a handler task to this scheduler.
 * @param {int} id the identity of the task
 * @param {int} priority the task's priority
 * @param {Packet} queue the queue of work to be processed by the task
 */
method addHandlerTask(id, priority, queue) {
  var ht = new HandlerTask;
  ht.setData(this);
  this.addTask(id, priority, queue, ht);
}

/**
 * Add a handler task to this scheduler.
 * @param {int} id the identity of the task
 * @param {int} priority the task's priority
 * @param {Packet} queue the queue of work to be processed by the task
 */
method addDeviceTask(id, priority, queue) {
  var dt = new DeviceTask;
  dt.setData(this);
  this.addTask(id, priority, queue, dt)
}

/**
 * Add the specified task and mark it as running.
 * @param {int} id the identity of the task
 * @param {int} priority the task's priority
 * @param {Packet} queue the queue of work to be processed by the task
 * @param {Task} task the task to add
 */
method addRunningTask(id, priority, queue, task) {
  this.addTask(id, priority, queue, task);
  this.currentTcb.setRunning();
}

/**
 * Add the specified task to this scheduler.
 * @param {int} id the identity of the task
 * @param {int} priority the task's priority
 * @param {Packet} queue the queue of work to be processed by the task
 * @param {Task} task the task to add
 */
method addTask(id, priority, queue, task) {
  this.currentTcb = new TaskControlBlock;
  this.currentTcb.setData(this.list, id, priority, queue, task);
  this.list = this.currentTcb;
  this.blocks.set(id, this.currentTcb);
}

/**
 * Execute the tasks managed by this scheduler.
 */
method schedule() {
  this.currentTcb = this.list;
  nativeWhile ({!isNone(this.currentTcb)}) {
    ifElse (this.currentTcb.isHeldOrSuspended()) {
      this.currentTcb = this.currentTcb.link;
    }
    {
      this.currentId = this.currentTcb.id;
      this.currentTcb = this.currentTcb.run();
    };
  }
}

/**
 * Release a task that is currently blocked and return the next block to run.
 * @param {int} id the id of the task to suspend
 */
method release(id) {
  var tcb = this.blocks.get(id);
  ifElse (isNone(tcb)) {
    tcb;
  }
  {
    tcb.markAsNotHeld();
    ifElse (tcb.priority > this.currentTcb.priority) {
      tcb;
    }
    {
      this.currentTcb;
    }
  }
}

/**
 * Block the currently executing task and return the next task control block
 * to run.  The blocked task will not be made runnable until it is explicitly
 * released, even if new work is added to it.
 */
method holdCurrent() {
  this.holdCount=this.holdCount+1;
  this.currentTcb.markAsHeld();
  this.currentTcb.link;
}

/**
 * Suspend the currently executing task and return the next task control block
 * to run.  If new work is added to the suspended task it will be made runnable.
 */
method suspendCurrent() {
  this.currentTcb.markAsSuspended();
  this.currentTcb;
}

/**
 * Add the specified packet to the end of the worklist used by the task
 * associated with the packet and make the task runnable if it is currently
 * suspended.
 * @param {Packet} packet the packet to add
 */
method queue(packet) {
  printLine("Scheduler.queue");
  var t = this.blocks.get(packet.id);
  ifElse (isNone(t)) {
    t;
  }
  {
    this.queueCount=this.queueCount+1;
    packet.link = NEWNONE;
    packet.id = this.currentId;
    t.checkPriorityAdd(this.currentTcb, packet);
  }
}

}
//End Scheduler





/**
 * A task control block manages a task and the queue of work packages associated
 * with it.
 * @param {TaskControlBlock} link the preceding block in the linked block list
 * @param {int} id the id of this block
 * @param {int} priority the priority of this block
 * @param {Packet} queue the queue of packages to be processed by the task
 * @param {Task} task the task
 * @constructor
 */
state TaskControlBlock {
  var link = unit;
  var id = unit;
  var priority = unit;
  var queue = unit;
  var task = unit;
  var currState = unit;

method setData(link, id, priority, queue, task){
  this.link = link;
  this.id = id;
  this.priority = priority;
  this.queue = queue;
  this.task = task;
  ifElse (isNone(queue)) {
    this.currState = STATE_SUSPENDED;
  }
  {
    this.currState = STATE_SUSPENDED_RUNNABLE;
  };
}

method setRunning() {
  this.currState = STATE_RUNNING;
}

method markAsNotHeld() {
  this.currState = this.currState & STATE_NOT_HELD;
}

method markAsHeld() {
  this.currState = this.currState | STATE_HELD;
}

method isHeldOrSuspended() {
  (this.currState != 0 && STATE_HELD != 0)  || (this.currState == STATE_SUSPENDED);
}

method markAsSuspended() {
  this.currState = this.currState | STATE_SUSPENDED;
}

method markAsRunnable() {
  this.currState = this.currState | STATE_RUNNABLE;
}

/**
 * Runs this task, if it is ready to be run, and returns the next task to run.
 */
method run() {
  printLine("tcb.run()");
  var packet = "";
  ifElse (this.currState == STATE_SUSPENDED_RUNNABLE) {
    packet = this.queue;
    this.queue = packet.link;
    ifElse (isNone(this.queue)) {
      this.currState = STATE_RUNNING;
    }
    {
      this.currState = STATE_RUNNABLE;
    };
  }
  {
    packet = NEWNONE;
  };
  this.task.run(packet);
}

/**
 * Adds a packet to the worklist of this block's task, marks this as runnable if
 * necessary, and returns the next runnable object to run (the one
 * with the highest priority).
 */
method checkPriorityAdd(task, packet) {
  var toReturn=task;
  ifElse (isNone(this.queue)) {
    this.queue = packet;
    this.markAsRunnable();
    if (this.priority > task.priority){
      toReturn=this;
    }
  }
  {
    this.queue = packet.addTo(this.queue);
  };
  toReturn;
}

method toString() {
  "tcb { " + this.id +": " + this.task + "@" + this.currState + " }";
}

}
//ends TaskControlBlock

/**
 * An idle task doesn't do any work itself but cycles control between the two
 * device tasks.
 * @param {Scheduler} scheduler the scheduler that manages this task
 * @param {int} v1 a seed value that controls how the device tasks are scheduled
 * @param {int} count the number of times this task should be scheduled
 * @constructor
 */
state IdleTask {
  var scheduler = unit;
  var v1 = unit;
  var count = unit;

method setData(scheduler, v1, count) {
  this.scheduler = scheduler;
  this.v1 = v1;
  this.count = count;
}

method run(packet) {
  this.count=this.count - 1;
  var toReturn = "";
  ifElse (this.count == 0) {
    toReturn = this.scheduler.holdCurrent();
  }
  {
    ifElse ((this.v1 & 1) == 0) {
      this.v1 = this.v1 >> 1;
      toReturn = this.scheduler.release(ID_DEVICE_A);
    }
    {
      this.v1 = (this.v1 >> 1) ^ 53256;
      toReturn = this.scheduler.release(ID_DEVICE_B);
    };
  };
  toReturn;
}

method toString() {
  "IdleTask";
}
}
//end IdleTask

/**
 * A task that suspends itself after each time it has been run to simulate
 * waiting for data from an external device.
 * @param {Scheduler} scheduler the scheduler that manages this task
 * @constructor
 */
state DeviceTask {
  var v1 = NEWNONE;
  var scheduler = unit;

method setData(scheduler) {
  this.scheduler = scheduler;
}

method run (packet) {
  var toReturn="";
  ifElse (isNone(packet)) {
    ifElse (isNone(this.v1)) {
      toReturn = this.scheduler.suspendCurrent();
    }
    {
    var v = this.v1;
    this.v1 = NEWNONE;
    toReturn = this.scheduler.queue(v);
    };
  }
  {
    this.v1 = packet;
    toReturn = this.scheduler.holdCurrent();
  };
}

method toString() {
  "DeviceTask";
}
}
//end DeviceTask

/**
 * A task that manipulates work packets.
 * @param {Scheduler} scheduler the scheduler that manages this task
 * @param {int} v1 a seed used to specify how work packets are manipulated
 * @param {int} v2 another seed used to specify how work packets are manipulated
 * @constructor
 */
state WorkerTask {
  var scheduler = unit;
  var v1 = unit;
  var v2 = unit;

method setData(scheduler, v1, v2) {
  this.scheduler = scheduler;
  this.v1 = v1;
  this.v2 = v2;
}

method run(packet) {
  var toReturn="";
  ifElse (isNone(packet)) {
    toReturn = this.scheduler.suspendCurrent();
  }
  {
    ifElse (this.v1 == ID_HANDLER_A) {
      this.v1 = ID_HANDLER_B;
    }
    {
      this.v1 = ID_HANDLER_A;
    };
    packet.id = this.v1;
    packet.a1 = 0;
    var i=0;
    nativeWhile ({i < DATA_SIZE;}) {
      this.v2=this.v2+1;
      if (this.v2 > 26) {
        this.v2 = 1;
      };
      packet.a2.set(i, this.v2);
      i=i+1;
    };
    toReturn = this.scheduler.queue(packet);
  };
  toReturn;
}

method toString() {
  "WorkerTask";
}
}
//end WorkerTask

/**
 * A task that manipulates work packets and then suspends itself.
 * @param {Scheduler} scheduler the scheduler that manages this task
 * @constructor
 */
state HandlerTask{
  var scheduler = unit;
  var v1 = unit;
  var v2 = unit;

	method setData(scheduler) {
	  this.scheduler = scheduler;
	  this.v1 = NEWNONE;
	  this.v2 = NEWNONE;
	}
	
	method run(packet) {
	  //this might be where problem is.  returning wrong item?
	  if (!isNone(packet)) {
		ifElse (packet.kind == KIND_WORK) {
		  this.v1 = packet.addTo(this.v1);
		}
		{
		  this.v2 = packet.addTo(this.v2);
		};
	  };
	  var toReturn="";
	  if (!isNone(this.v1)) {
		var count = this.v1.a1;
		var v="";
		ifElse (count < DATA_SIZE) {
		  if (!isNone(this.v2)) {
			v = this.v2;
			this.v2 = this.v2.link;
			v.a1 = this.v1.a2.get(count);
			this.v1.a1 = count + 1;
			toReturn = this.scheduler.queue(v);
		  }
		}
		{
		  v = this.v1;
		  this.v1 = this.v1.link;
		  toReturn = this.scheduler.queue(v);
		};
	  };
	  if (toReturn==""){
		toReturn = this.scheduler.suspendCurrent();
	  };
	  toReturn;
	}
	
	method toString() {
	  "HandlerTask";
	}
}
//end HandlerTask

/* --- *
 * P a c k e t
 * --- */

var DATA_SIZE = 4;

/**
 * A simple package of data that is manipulated by the tasks.  The exact layout
 * of the payload data carried by a packet is not importaint, and neither is the
 * nature of the work performed on packets by the tasks.
 *
 * Besides carrying data, packets form linked lists and are hence used both as
 * data and worklists.
 * @param {Packet} link the tail of the linked list of packets
 * @param {int} id an ID for this packet
 * @param {int} kind the type of this packet
 * @constructor
 */
state Packet {
  var link = unit;
  var id = unit;
  var kind = unit;
  var a1 = unit;
  var a2 = unit;

method setData(link, id, kind) {
  this.link = link;
  this.id = id;
  this.kind = kind;
  this.a1 = 0;
  this.a2 = makeArray(DATA_SIZE);
}

/**
 * Add this packet to the end of a worklist, and return the worklist.
 * @param {Packet} queue the worklist to add this packet to
 */
method addTo(queue) {
  this.link = NEWNONE;
  ifElse (isNone(queue)) {
    this;
  }
  {
  var peek = queue;
  var next = queue;
  nativeWhile ({!isNone(peek)}) {
    next = peek;
    peek=next.link;
  };
  next.link = this;
  queue;
  };
}

method toString() {
  "Packet";
}
}
//end Packet


method isNone(value) {
    match(value) {
        case None {
            true;
        }
        default {
            false;
        }
    }
}

method main() {
	printLine("Plaid");
	var time=0;
	var i=0;
	printLine("Start: " + java.util.Date.new().toString());
	nativeWhile({i<10}){
		runRichardsCompiled();
		printLine(java.util.Date.new().toString());
		i=i+1;
	};
}

