/**
 * Copyright (c) 2010 The Plaid Group (see AUTHORS file)
 * 
 * This file is part of Plaid Programming Language.
 *
 * Plaid Programming Language is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 *  Plaid Programming Language is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with Plaid Programming Language.  If not, see <http://www.gnu.org/licenses/>.
 */

package plaid.lang.testing;

import plaid.collections.LinkedList;

state TestFactory {
    method unique LinkedList createTests();

    method unique Test mktest(immutable String name, /*Test -> void*/ body) {
		new Test {
			val name = name;
			val body = body;
			var pass = true;

			method immutable String getName() { this.name }
			method immutable Boolean run(immutable Boolean verbose) {
				
				this.body(this);
				val result = match (this.pass) {
					case True { "PASS" } 
					case False { "FAIL "}
				};
				match (verbose) {
					case True {
						printLine(">>> BEGIN: " + this.name);
						printLine("--- RESULT: " + result);
						printLine("<<< END");
					}
					case False {/* no op */ }
				};
				
				this.pass
			}

			method void assertFn(() -> immutable Boolean assertion) {
				this.pass = this.pass && assertion();
			}

			method void assertTrue(immutable Boolean assertion) {
				this.pass = this.pass && assertion;
			}

			method void assertFalse(immutable Boolean assertion) {
				this.pass = this.pass && (!assertion);
			}

			method void fail() {
				this.pass = false
			}
		}
	}
}