package plaid.ast;

state Binding case of Expression = {
	val immutable Token token;
	val unique ID x;
	val unique Expression exp;
	val unique Expression body;
	val immutable Boolean mutable;
	
	// accept() and copy() are left unimplemented.
	
	method unique Binding copyBinding() {
		val copy = makeBinding(this.token, this.x.copy(), this.exp.copy(), this.body.copy(), this.mutable);
		match (this) {
			case NoTypeAnnotation { /*nothing to do*/ }
			case ObjectTypeAnnotation { copy <- ObjectTypeAnnotation { val oType = this.oType.copy(); } }
			case PermTypeAnnotation { copy <- PermTypeAnnotation { val pType = this.pType.copy(); } }
		};
		copy
	}
	
	method immutable Boolean equalBinding(immutable Binding other)[immutable Binding] {
		this.x == other.x  &&
		this.exp == other.exp &&
		this.body == other.body &&
		this.mutable == other.mutable &&
		this.equalAnnotation(other)
	}
	
	method immutable String bindingString()[immutable Binding] {
		x.toString() + this.annotationString() + "= " + this.exp.toString() + " in " + body.toString() 
	}
	
} with Annotation