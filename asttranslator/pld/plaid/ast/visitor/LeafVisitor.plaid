package plaid.ast.visitor;

state LeafVisitor case of ASTVisitor {
	method visitApplication(node) {
		node.f.accept(this);
		node.arg.accept(this)
	}
	
	method visitAssignment(node) {
		match (node.target) {
			case Some {
				node.target.value.accept(this);
			}
			default {}
		};
		node.field.accept(this);
		node.value.accept(this)		
	}
	
	method visitCase(node) {
		node.qi.accept(this);
		node.x.accept(this);
		node.e.accept(this)
	}
	
	method visitChangeState(node) {
		node.e.accept(this);
		node.s.accept(this)
	}
	
	method visitCompilationUnit(node) {
		node.decls.map(fn (decl) => {
			decl.accept(this)
		});
		node.imports.accept(this)
	}
	
	method visitDeclList(node) {
		node.decls.map(fn (decl) => {
			decl.accept(this)
		});
	}
	
	method visitDereference(node) {
		node.left.accept(this);
		node.right.accept(this)
	}
	
	method visitDestructiveDereference(node) {
		node.left.accept(this);
		node.right.accept(this)
	}
	
	method visitFieldDecl(node) {
		node.f.accept(this);
		node.e.accept(this)
	}
	
	method visitID(node) {
	}
	
	method visitImportList(node) {
//		node.imports.map(fn (im) => {
//			im.accept(this)
//		});
	}
	
	method visitIntLiteral(node) {
	}
	
	method visitJoin(node) {
		node.body.accept(this);
	}
	
	method visitLambda(node) {
		node.x.accept(this);
		node.body.accept(this)
	}
	
	method visitLetBinding(node) {
		node.x.accept(this);
		node.exp.accept(this);
		node.body.accept(this)
	}
	
	method visitMatch(node) {
		node.e.accept(this);
		node.caseList.map(fn (c) => {
			c.accept(this)
		});
	}
	
	method visitMethodDecl(node) {
		node.body.accept(this);
		match (node.arg) {
			case Some {
				node.arg.value.accept(this);
			}
			default {}
		};
	}
	
	method visitMethodTypeDecl(node) {
	}
	
	method visitNewInstance(node) {
		node.st.accept(this)
	}
	
	method visitNullNode(node) {
	}
	
	method visitPermType(node) {
	}
	
	method visitQI(node) {
	}
	
	method visitSplit(node) {
		node.body.accept(this);
	}
	
	method visitStateDecl(node) {
		node.name.accept(this);
		node.stateDef.accept(this);
		match (node.caseOf) {
			case Some {
				node.caseOf.value.accept(this);
			}
			default {}
		};
	}
	
	method visitStringLiteral(node) {
	}
	
	method visitUnitLiteral(node) {
	}
	
	method visitWith(node) {
		node.s1.accept(this);
		node.s2.accept(this)
	}
}