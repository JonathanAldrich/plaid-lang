package plaid.ast.types;

import plaid.lang.*;

/**
 *	represents a lambda type as an object type
 *	this could also be represented as a PlaidType, but instead for
 *	consistency and flexibility it is an ObjectType.  Currently,
 *	all LambdaType objects are considered to have an Immutable 
 *	permission, but later we could allow other permissions under
 *	this structure.
 */
state LambdaType case of ObjectType {

	var /*TakeBinding*/ returnType; //TODO: change back to val when have restricted update state change working for PermType <- PermType
	val /*List<ChangeType>*/ argTypes;
	val /*Map<ID,ChangeType>*/ environmentTypes;
	
	/**
	 * Lambda types have no members 
	 */
	method /* unique Option<immutable DeclType>*/ 
		contains(immutable ID f, unique StateTable st)[immutable LambdaType] 
	{
		new None
	}
	
	/**
	 * lambda types have no member
	 */
	method /* unique Option<immutable DeclType> */ 
		declaredType(immutable ID f, unique StateTable st)[immutable LambdaType]
	{
		new None
	}
	
	/**
	 *  lambda types have no members, so updates mean nothing
	 */
	method void memberUpdate(immutable ID f, immutable Binding >> void b, unique StateTable st)
							[unique LambdaType]
	{ /*no op*/ }
	
	/**
	 *  lambdas are not borrowed
	 */
	method immutable Boolean notBorrowed()[immutable LambdaType] {
		true
	}
	
	/**
	 *  packing does nothing
	 */
	method void pack(unique StateTable st)[unique LambdaType] {
		/* no op */
	}
	
	/**
	 *  constructs the least upper Bound of this type and the other type.  The state table is used
	 *  for nominal types.  We return an option because comparing nominal and function types
	 *  does not have an upper bound
	 */
	method /* unique Option<unique ObjectType>*/ 
		leastUpperBound(immutable ObjectType other, unique StateTable st)[immutable LambdaType]
	{
		match (other) {
			case LambdaType {
				ifElse (this.argTypes.length() == other.argTypes.length()) {
					ifElse (this.environmentTypes.size() == other.environmentTypes.size()) {
						val /*List<Pair<ChangeType,ChangeType>>*/ argZip = this.argTypes.zip(other.argTypes);
						val /*List<ChangeType>*/ joinedArgs = argZip.map(fn (p) => {
							//TODO : FIXME - update to new rep of Change Type
							new ChangeType {
								val input = p.fst.input.leastUpperBound(p.snd.input, st);
								val output = p.fst.output.leastUpperBound(p.snd.output, st);
							}
						});
						var /*boolean*/ envOk = true;
						val /*Map<ID,ChangeType>*/ joinedEnv = java.util.HashMap.new();
						val envIter = this.environmentTypes.keySet().iterator();
						while { envOk && envIter.hasNext() } {
							val /*ID*/ envID = envIter.next()
							ifElse (other.environmentTypes.containsKey(envID)) {
								val /*ChangeType*/ thisEnvCT = this.environmentTypes.get(envID);
								val /*ChangeType*/ otherEnvCT = other.environmentTypes.get(envID);
								joinedEnv.put(envID,new ChangeType {
									val input = thisEnvCT.input.leastUpperBound(otherEnvCT.input,st);
									val output = thisEnvCT.output.leastUpperBound(otherEnvCT.output, st);
								});
							} {
								//java.lang.System.out.println("JOIN FAILED: both lambdas do not specify environment variable " + envID.name);
								envOk = false;
							}
						};
						ifElse (envOk) {
							//success!
							val /*PermType*/ joinedReturnType = this.returnType.leastUpperBound(other.returnType, st);
							createSome(new LambdaType {
								var returnType = joinedReturnType;
								val argTypes = joinedArgs;
								val environmentTypes = joinedEnv;
							})
						} {
							//error elsewhere, return generic lambda type
							new None
						}
					} {
						//java.lang.System.out.println("JOIN FAILED: lambdas do not have the same number of environment variables");
						new None
					}
				} {
					//java.lang.System.out.println("JOIN FAILED: lambdas do not have the same number of arguments");
					new None
				}
			}
			default {
				//java.lang.System.out.println("JOIN FAILED: lambda can only join with another lambda");
				new None
			}
		}
	}
	
	/**
	 *  Object subtyping
	 */
	method immutable Boolean isSubObjectTypeOf(immutable ObjectType other, unique StateTable st)
											  [immutable NominalType]
	{
		match (other) {
			case LambdaType {
				//requirements:
				
				//1) this.returnType <: other.returnType
				val /*Boolean*/ retTypeOk = this.returnType.isSubTypeBindingOf(other.returnType, st); //error message elsewhere
				
				//2) same number and compatible arg types
				var /*Boolean*/ argsOk = this.argTypes.length() == other.argTypes.length();
				ifElse (argsOk) {
					val /*List<Pair<ChangeType,ChangeType>>*/ pairedArgs = this.argTypes.zip(other.argTypes);
					argsOk = pairedArgs.foldr(fn (p,z) => p.fst.isSubChangeTypeOf(p.snd,st) && z, true);
				} {
					//java.lang.System.out.println("SUBTYPING FAILED : mismatched number of args");
				};
				
				//4) same environment bindings (names) and compatible types
				
				//environments must be the same size (for now)
				var /*boolean*/ envOk = this.environmentTypes.size() == other.environmentTypes.size();
				//
				val /*Map<ID,ChangeType>*/ joinedEnv = java.util.HashMap.new();
				val envIter = this.environmentTypes.keySet().iterator();
				while { envOk && envIter.hasNext() } {
					val /*ID*/ envID = envIter.next()
					ifElse (other.environmentTypes.containsKey(envID)) {
						val /*ChangeType*/ thisEnvCT = this.environmentTypes.get(envID);
						val /*ChangeType*/ otherEnvCT = other.environmentTypes.get(envID);
						envOk = this.EnvCT.isSubChangeTypeOf(otherEnvCT,st)
					} {
						//java.lang.System.out.println("JOIN FAILED: both lambdas do not specify environment variable " + envID.name);
						envOk = false;
					}
				};
				
				retTypeOk && argsOk && envOk;
			}
			default { 
				//java.lang.System.out.println("TYPECHECK FAILED: lambda must subtype another lambda");
				false 
			}
		}
	}											 
	
	/**
	 *  ObjectType downgrading downgrades the objectType of any field that currently has a weak permission
	 *  Note that we currently assume that Nominal Types cannot have fields with weak permissions
	 */
	method void downgrade()[unique ObjectType];
	
	method ==(other) {
		match (other) {
			case LambdaType {
				this.returnType == other.returnType
				&& this.argTypes.zip(other.argTypes)
				                   .map(fn (p) => { p.first() == p.second(); })
				                   .foldr(fn (x, y) => { x && y; }, true);
			}
			default {
				false;
			}
		};
	}
	
	method /*boolean*/ hasArg() {
		match (argTypes.head) {
			case Nil { false }
			case Cons { true }
		}
	}
	
	method toString() {
		var /*String*/ envTypeString = "";
		val envIter = environmentTypes.keySet().iterator();
		while { envIter.hasNext() } {
			val /*ID*/ envVar = envIter.next();
			val /*ChangeType*/ envVarType = environmentTypes.get(envVar);
			envTypeString = envTypeString + ", " + envVarType.toString() + " " + envVar.name;
		};
		if (envTypeString.length() > 0) {
			envTypeString = "[" + envTypeString.substring(2) + "]";
		};
		
		var /*String*/ args = "";
		ifElse (this.hasArg()) {
			args = this.argTypes.foldr(fn (a,r) => ", " + a.toString() + r, "");
			args = "(" + args.substring(2) + ")";
		} {
			args = "()";
		};
		
		args + envTypeString + " -> " + returnType.toString();
	}
	
	method /*Boolean*/ subtypeOf(/*Type*/ other, /*StateTable*/ stateTable) {
		
	}
	
	//try to find the least upper bound on the two lambda types
	//for arguments and environment variables, we need:
	//	most specific input (the other type must be a supertype and we need the weakest permission)
	//	least specific output (the other must be a subtype)
	//need the same number of arguments
	//in terms of environment variables they must have the same list //TODO: can we do better than this?
	method /*Type*/ join(/*Type*/ other, /*StateTable*/ stateTable) {
		
	}
}