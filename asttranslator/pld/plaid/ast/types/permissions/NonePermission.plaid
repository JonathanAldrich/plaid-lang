package plaid.ast.types.permissions;

/**
 *	None permission
 */
state NonePermission case of Permission {
	method isWritable() {
		false;
	}

	method splitPerm(p2) {
		match(p2) {
			// none => none/none
			case NonePermission {
				new Some{ val value = this; };
			}
			default {
				new None;
			}
		};
	}
	
	method ==(p2) {
		match(p2) {
			case NonePermission {
				true;
			}
			default {
				false;
			}
		};
	}
	
	//always take the other one
	method join(p2) {
		p2;
	}
	
	//could only join to None
	method joinTo(p2,target) {
		match (target) {
			case NonePermission { createSome(target) }
			default { NONE }
		}
	}
	
	//always weaker
	method asStrongAs(p2) {
		match (p2) {
			case NonePermission { true }
			default { false }
		}
	}
	
		method immutable Permission leastUpperBound(p2) {
		match (p2) {
			case DynPermission { DYNP }
			case UniquePermission { NONEP }
			case FullPermission { NONEP }
			case ImmutablePermission { NONEP }
			case SharedPermission { NONEP }
			case PurePermission { NONEP }
			case NonePermission { NONEP }
			default { 
				java.lang.System.out.println("not a permission"); 
				DYNP; 
			}
		}
	}
	
	method immutable Permission greatestLowerBound(p2) {
		match (p2) {
			case DynPermission { DYNP }
			case UniquePermission { UNIQUE }
			case FullPermission { FULL }
			case ImmutablePermission { IMMUTABLE }
			case SharedPermission { SHARED }
			case PurePermission { PURE }
			case NonePermission { NONEP }
			default { 
				java.lang.System.out.println("not a permission"); 
				NONEP; 
			}
		}
	}
	
	//weaker
	method /*Permission*/ merge(p2) {
		NONEP //always weaker
	}
	
	method toString() {
		"none"
	}
}