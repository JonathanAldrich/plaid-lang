package plaid.ast.types;

/* A TakeBinding is a case of TypeBinding used for tracking the types of
   locations in a Plaid program.  It has no additional information other
   than the current PlaidType (currentType field) inherited from TypeBinding.
 */
 
 state TakeBinding case of TypeBinding {
 
 	method /*unique Option<unique TypeBinding>*/ 
 		leastUpperBound(immutable TypeBinding other, unique StateTable st)[immutable TakeBinding]
 	{
 		match (other) {
 			case TakeBinding {
 				val /*unique Option<unique PermType>*/ newPermType = 
 					other.currentType.leastUpperBound(this.currentType,st);
 				match(newPermType) {
 					case Some {
 						createSome(
 							new TakeBinding { 
 								var unique PermType currentType = newPermType.value;
 							}
 						)
 					}
 					case None { new None }
 				}
 			}
 			case BorrowBinding { new None }
 		}
 	}
 	 
 	 method /*unique Option<unique TypeBinding>*/ 
 		greatestLowerBound(immutable TypeBinding other, unique StateTable st)[immutable TakeBinding]
 	{
 		match (other) {
 			case TakeBinding {
 				val /*unique Option<unique PermType>*/ newPermType = 
 					other.currentType.greatestLowerBound(this.currentType,st);
 				match(newPermType) {
 					case Some {
 						createSome(
 							new TakeBinding { 
 								var unique PermType currentType = newPermType.value;
 							}
 						)
 					}
 					case None { new None }
 				}
 			}
 			case BorrowBinding { new None }
 		}
 	}
 
 	method immutable Boolean ==(immutable TypeBinding other) {
 		match (other) {
 			case TakeBinding {
 				this.currentType == other.currentType
 			}
 			default { false }
 		}
 	}
 	
 	method toString() {
 		currentType.toString();
 	}
 	
 	/**
 	 *	the current type of this must be a subtype of the currentType of other
 	 *  as long as other is also a take binding
 	 */
 	method immutable Boolean isSubTypeBindingOf(immutable TypeBinding other, stateTable)
 											   [immutable TakeBinding] 
	{
 		match (other) {
 			case TakeBinding {
 				this.currentType.isSubPermTypeOf(other.currentType, stateTable)
 			}
 			default { false }
 		}
 	}
 
 }