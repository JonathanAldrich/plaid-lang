package plaid.testing;

import java.lang.System;

state Tester {
	var numTests = 0;
	var numFailures = 0;
	
	method void testPassedMsg(immutable Boolean t, immutable String >> void s)[unique Tester] {
		this.numTests = this.numTests + 1;
		System.out.print("\t\tTest Passed? ");
		match (t) {
			case True { System.out.print("yes"); }
			case False { System.out.print("no");  this.numFailures = this.numFailures+1 }
		};
		match (s.equals("")) {
			case True { System.out.println("") }
			case False { System.out.println(" - " + s) }
		};
	}

	method void testPassed(immutable Boolean t)[unique Tester] { testPassedMsg(t,"") }

	method void printSummary()[immutable Tester] {
		System.out.println("" + this.numFailures + "/" + this.numTests + " tests failed");
	}
	
	method void reset()[unique Tester] {
		this.numTests = 0;
		this.numFailures = 0;
	}

}