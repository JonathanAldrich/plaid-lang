package coreExamples.benchmarks.splaytree;

state SplayBenchmarkUtils {
	val /* immutable Integer */ kSplayTreeSize = 1000;
	val /* immutable Integer */ kSplayTreeModifications = 80;
	val /* immutable Integer */ kSplayTreePayloadDepth = 3;
	
	var /* unique SplayTree */ tree;

	method /* */ generatePayloadTree(immutable Integer depth, immutable String tag) {
		ifElse (depth == 0) {
			new PayloadLeaf { val str = "String for key " + tag + " in leaf node"; };
		}{
			val l = generatePayloadTree(depth - 1, tag);
			val r = generatePayloadTree(depth - 1, tag);
			new PayloadNode { val left = l; val right = r; };
		};
	}

	method unit insertNewNode() {
		var rand = java.util.Random.new();
		var key = rand.nextDouble();
		while { this.tree.containsKey(key) } {
			key = rand.nextDouble();
		};
		var payload = generatePayloadTree(this.kSplayTreePayloadDepth, key.toString());
		this.tree.insert(key, payload);
		key;
	}
	
	method unit splayTreeSetup() {
		this.tree = new SplayTree;
		var i = 0;
		while { i < this.kSplayTreeSize } {
			this.insertNewNode();
			i = i + 1;
		};
	}
	
	method unit splayTreeTeardown() {
		var keys = java.util.ArrayList.new();
		this.tree.exportKeys(keys);
		ifElse (keys.size() != kSplayTreeSize) {
			java.lang.System.out.println("ERROR: Splay tree has wrong size!");
			java.lang.System.out.println("Expected size: " + kSplayTreeSize);
			java.lang.System.out.println("Actual size: " + keys.length());
		}{
			java.lang.System.out.println("Splay tree has correct size.");
		};
		this.splayTreeTeardownHelper(keys);
	}
	
	method unit splayTreeTeardownHelper(list) {
		/* look at the head of the list */
		var i = 1;
		while { i < list.size() } {
			val lastKey = list.get(i - 1);
			val key = list.get(i);
			if (lastKey > key) {
				java.lang.System.out.println("ERROR: keys not in order!");
				java.lang.System.out.println("" + i + ": prev key = " + lastKey + ", curr key = " + key);
				this.printKeys(list);
				// set i so we break out of the loop
				i = list.size() + 1;
			};
			i = i + 1;
		};
		
		// if we iterated all the way through without upsetting
		// anything, then i will be equal to the size of the list
		ifElse (i == list.size()) {
			java.lang.System.out.println("Test passed.");
		}{
			java.lang.System.out.println("Test failed!.");
		};
	}
	
	method unit printKeys(/* ArrayList */ list) {
		java.lang.System.out.println("[");
		var i = 0;
		while { i < list.size() } {
			java.lang.System.out.println("\t" + list.get(i) + ",");
			i = i + 1;
		};
		java.lang.System.out.println("]");
	}
	
	method unit splayRun() {
		var i = 0;
		while { i < this.kSplayTreeModifications } {
			var key = this.insertNewNode();
			var greatestOpt = tree.findGreatestLessThan(key);
			match(greatestOpt) {
				case Some {
					tree.remove(greatestOpt.value.key);
				}
				case None {
					tree.remove(key);
				}
			};
			i = i + 1;
		};
	}
}