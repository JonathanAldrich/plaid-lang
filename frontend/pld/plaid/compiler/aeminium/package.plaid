package plaid.compiler.aeminium;

import plaid.compiler.*;

/*****************************************************************************
 ** global type map Symbol -> DependecyInformation
 ****************************************************************************/
var shared Map aeminiumSymbolToDepsMap = java.util.IdentityHashMap.new();

method void setDepInfoForSymbol(immutable Symbol symbol, unique DependencyInformation info) {
     aeminiumSymbolToDepsMap.put(symbol, info);
}

method immutable Option depInfoForSymbol(immutable Symbol symbol) {
    ifElse (aeminiumSymbolToDepsMap.containsKey(symbol) ) {
        makeSome(aeminiumSymbolToDepsMap.get(symbol));
    }{
        NONE
    }
}


/*****************************************************************************
 ** global type map ASTNode -> DependecyInformation
 ****************************************************************************/
var shared Map aeminiumASTToDepsMap = java.util.IdentityHashMap.new();

method void addDepInfoForAST(immutable ASTNode node, unique DependencyInformation info) {
     aeminiumASTToDepsMap.put(node, info);
}

method immutable Option depInfoForAST(immutable ASTNode node) {
    ifElse (aeminiumASTToDepsMap.containsKey(node) ) {
        makeSome(aeminiumASTToDepsMap.get(node));
    }{
        NONE
    }
}


/*****************************************************************************
 ** global type map SourceLocation -> DependecyInformation
 ****************************************************************************/
var shared Map aeminiumSLocToDepInfo = java.util.IdentityHashMap.new();

method void addDepInfoForSLoc(immutable SourceLocaction sloc, unique DependencyInformation info) {
     aeminiumSLocToDepInfo.put(sloc, info);
}

method immutable Option depInfoForSLoc(immutable SourceLocation sloc) {
    ifElse (aeminiumSLocToDepInfo.containsKey(sloc) ) {
        makeSome(aeminiumSLocToDepInfo.get(sloc));
    }{
        NONE
    }
}


/*****************************************************************************
 ** helper functions
 ****************************************************************************/

method unique Set findLastDependentsForSymbol(shared DependencyInformation depInfo,
                                              shared Symbol symbol,
                                              immutable Boolean onlyAST) {
                                              
    //DEBUG(DEBUG_PRIO_LOW, "<findLastDependents> for " + depInfo.toString() + " astOnly="+onlyAST);
    val set = java.util.HashSet.new();
    
    val immutable Boolean valid = match (depInfo) {
        case ASTInformation   { true }
        case JoinInformation  { depInfo.symbol == symbol }
        case SplitInformation { depInfo.symbol == symbol }
        case FieldRestoreInformation { depInfo.symbol == symbol } 
    };
    
    if ( valid ) {
        // try to find recursively find last dependent
        val it = depInfo.rdeps.iterator();
        while {it.hasNext()}{
            val recursiveSet = findLastDependentsForSymbol(it.next(), symbol, onlyAST);
            set.addAll(recursiveSet)
        };
        // add current node if recursive serach failed 
        if ( set.size() == 0 ) {
            match (onlyAST) {
                case True {
                    match (depInfo) {
                        case ASTInformation { set.add(depInfo) } 
                        default { }
                    }
                }
                case False {
                    set.add(depInfo);
                }
            }
            
        };
    };
     
    set
}

method void addDependency(shared DependencyInformation from,
                          shared DependencyInformation to) {
    //DEBUG(DEBUG_PRIO_LOW, "<addDependency> " + from.toString() + " -> " + to.toString() );
    from.deps.add(to);
    to.rdeps.add(from);
}

method void removeDependency(shared DependencyInformation from,
                             shared DependencyInformation to) {
    //DEBUG(DEBUG_PRIO_LOW, "<addDependency> " + from.toString() + " -> " + to.toString() );
    from.deps.remove(to);
    to.rdeps.remove(from);
}


method void removeDependencyInformation(shared DependencyInformation depInfo)  {
    DEBUG(DEBUG_PRIO_HIGH, "REMOVE " + depInfo.toString() );
    
    // remove node from dependents 
    val rdepsIt = depInfo.rdeps.iterator();
    while { rdepsIt.hasNext() }{
        val shared DepenendencyInformation rdep = rdepsIt.next();

        // connect to deps of DepInfo
        val depsIt = depInfo.deps.iterator();
        while { depsIt.hasNext() }{
            val shared DependencyInformation dep = depsIt.next();
            
            // add connection between rdep -> dep
            addDependency(rdep, dep);
        }
    };

    // remove connections
    val removeTo = java.util.HashSet.new();
    removeTo.addAll(depInfo.deps);
    val removeToIt  = removeTo.iterator();
    while { removeToIt.hasNext() }{ removeDependency(depInfo, removeToIt.next()) };

    val removeFrom = java.util.HashSet.new();
    removeFrom.addAll(depInfo.rdeps);
    val removeFromIt  = removeFrom.iterator();
    while { removeFromIt.hasNext() }{ removeDependency(removeFromIt.next(), depInfo) };

}

method void dumpGraph(immutable ParsedDecl declNode, immutable String suffix) {
    DEBUG_DO(DEBUG_PRIO_MEDIUM, fn ()  => {
        val ggg = new GraphvizGraphGenerator { val declNode = declNode; };
        
        val immutable String dump = ggg.dump();
    
        // dump file 
        val file = java.io.File.new(ggg.getPath() + "." + suffix +".dot");
        val fw = java.io.FileWriter.new(file);
        fw.write(dump);
        fw.flush();
        fw.close();
    });
}


method unique Set getSplitInformation(shared Set infos) {
  val unique Set result = java.util.HashSet.new();
  
  val it = infos.iterator();
  while { it.hasNext() }{
      val next = it.next();
      match ( next ) {
          case SplitInformation { result.add(next) }
          default {}
      }
  };
  
  result;
}

method unique Set getJoinInformation(shared Set infos) {
  val unique Set result = java.util.HashSet.new();
  
  val it = infos.iterator();
  while { it.hasNext() }{
      val next = it.next();
      match ( next ) {
          case JoinInformation { result.add(next) }
          default {}
      }
  };
  
  result;
}

/*****************************************************************************
 ** factory methods
 ****************************************************************************/
 
 var immutable Integer depInfoIdCounter = 0;
 
 method immutable Integer nextDepInfoId() {
     depInfoIdCounter = depInfoIdCounter + 1;
     depInfoIdCounter
 }
 
 method unique ASTInformation makeASTInformation(immutable ASTNode node) {
    new ASTInformation {
        val id    = nextDepInfoId();
        val node  = node;
    }
 }
 
 
method unique AtomicEnterInformation makeAtomicEnterInformation(immutable ASTNode node) {
    new AtomicEnterInformation {
        val id    = nextDepInfoId();
        val node  = node;
    }
 }
 
 method unique AtomicLeaveInformation makeAtomicLeaveInformation(immutable AtomicEnterInformation enterInfo) {
    new AtomicLeaveInformation {
        val id    = nextDepInfoId();
        val enterInfo  = enterInfo;
    }
 } 
 
method unique MatchEnterInformation makeMatchEnterInformation(immutable ASTNode node) {
    new MatchEnterInformation {
        val id    = nextDepInfoId();
        val node  = node;
    }
 }
 
 method unique MatchLeaveInformation makeMatchLeaveInformation(immutable MatchEnterInformation enterInfo) {
    new MatchLeaveInformation {
        val id    = nextDepInfoId();
        val enterInfo  = enterInfo;
    }
 }
 
 method unique SpitInformation makeSplitInformation(shared Symbol symbol,
                                                    immutable Permission permIn,
                                                    immutable Permission permOut) {
    new SplitInformation {
        val id      = nextDepInfoId();
        val symbol  = symbol;
        val permIn  = permIn;
        val permOut = permOut;
    }
}

method unique JoinInformation makeJoinInformation(shared Symbol symbol,
                                                  immutable Permission permIn,
                                                  immutable Permission permOut) {
    new JoinInformation {
        val id      = nextDepInfoId();
        val symbol  = symbol;
        val permIn  = permIn;
        val permOut = permOut;
    }
}

method unique FieldRestoreInformation makeFieldRestoreInformation(shared Symbol symbol,
                                                                  immutable String fieldName,
                                                                  immutable Permission permIn,
                                                                  immutable Permission permOut) {
    new FieldRestoreInformation {
        val id      = nextDepInfoId();
        val symbol  = symbol;
        val fieldName= fieldName;
        val permIn  = permIn;
        val permOut = permOut;
    }
}