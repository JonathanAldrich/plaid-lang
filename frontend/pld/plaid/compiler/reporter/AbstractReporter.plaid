package plaid.compiler.reporter;

import plaid.compiler.Reporter;
import java.io.File;
import plaid.collections.makeLinkedList;

state AbstractReporter case of Reporter {
    val unique List errors = makeLinkedList();
    val unique List warnings = makeLinkedList();
    val unique List infos = makeLinkedList();

    method immutable Boolean wereErrors() {
        this.errors.size() > 0;
    }

    method void clearErrors() {
        this.errors.clear();
        this.warnings.clear();
        this.infos.clear();
    }
    

    //=======================================================================
    // errors 
    //=======================================================================
    method void error(immutable File file,
                      immutable Integer beginLine,
                      immutable Integer beginColumn,
                      immutable Integer endLine,
                      immutable Integer endColumn,
                      immutable String message) {
         this.errors.add(
            new Error {
                val file        = file;
                val beginLine   = beginLine;
                val beginColumn = beginColumn;
                val endLine     = endLine;
                val endColumn   = endColumn;
                val message     = message;
            }
         )                
    }
     
     
    method void errorToken(immutable Token token,
                           immutable String message) {
        error(java.io.File.new(token.fileName),
              token.beginLine,
              token.beginColumn,
              token.endLine,
              token.endColumn,
              message)                      
    }
 
 
    //=======================================================================
    // warnings 
    //=======================================================================
    method void warning(immutable File file,
                      immutable Integer beginLine,
                      immutable Integer beginColumn,
                      immutable Integer endLine,
                      immutable Integer endColumn,
                      immutable String message) {
         this.warnings.add(
            new Warning {
                val file        = file;
                val beginLine   = beginLine;
                val beginColumn = beginColumn;
                val endLine     = endLine;
                val endColumn   = endColumn;
                val message     = message;
            }
         )                
    }
     
    method void warningToken(immutable Token token,
                             immutable String message) {
        warning(java.io.File.new(token.fileName),
                token.beginLine,
                token.beginColumn,
                token.endLine,
                token.endColumn,
                message)                               
    }
                        
    //=======================================================================
    // info 
    //=======================================================================
    method void info(immutable File file,
                      immutable Integer beginLine,
                      immutable Integer beginColumn,
                      immutable Integer endLine,
                      immutable Integer endColumn,
                     immutable String message) {
         this.infos.add(
            new Info {
                val file        = file;
                val beginLine   = beginLine;
                val beginColumn = beginColumn;
                val endLine     = endLine;
                val endColumn   = endColumn;
                val message     = message;
            }
         )                
    }
     
    method void infoToken(immutable Token token,
                          immutable String message) {
        info(java.io.File.new(token.fileName),
             token.beginLine,
             token.beginColumn,
             token.endLine,
             token.endColumn,
             message)   
    }
}