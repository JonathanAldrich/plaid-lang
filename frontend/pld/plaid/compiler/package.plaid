
package plaid.compiler;

import plaid.compiler.Reporter;
import plaid.compiler.reporter.*;

import plaid.compiler.resolver.ClasspathResolver;

import plaid.compiler.context.*;
import plaid.compiler.passes.*;

import plaid.compiler.util.classpathToList;

import java.lang.System;


var immutable Integer DEBUG_LEVEL = 0;

method void INFO(immutable String msg) {
    if ( 0 < DEBUG_LEVEL ) {
        printLine("[INFO   ] " + msg);
    }
}

method void WARNING(immutable String msg) {
    if ( 0 < DEBUG_LEVEL ) {
        printLine("[WARNING] " + msg);
    }
}

method void ERROR(immutable String msg) {
    printLine("[ERROR  ] " + msg);
}

val DEBUG_PRIO_HIGH   = 1;
val DEBUG_PRIO_MEDIUM = 2;
val DEBUG_PRIO_LOW    = 3;

method void DEBUG(immutable Integer level, immutable String msg) {
    if ( level <= DEBUG_LEVEL) {
       printLine("[DEBUG  ] " + msg);
    }
}


method showUsage() {
    printLine("usage: plaidc [OPTIONS] [FILES]");
    printLine("");
    printLine("Options:");
    printLine(" -d | --debug  <LEVEL>       Set the default level for debug output (0,1,2,3).");
    printLine(" -f | --force-recompile      Generate output files even if source files are older.");
    printLine(" -h | --help                 This message.");
    printLine(" -i | --input  <DIR>         The directory of Plaid files to compile.");
    printLine(" -k | --keep                 Keep temporary files.");
    printLine(" -o | --output <DIR>         The directory to put generated files.");
    printLine(" -p | --plaidpath <DIR>      A path where plaid sources are stored.");
    printLine(" -t | --temp <DIR>           The directory to put temporary generated files.");
    printLine(" -v | --version              The version of the current compiler.");
    
}


method showVersion() {
    printLine("Plaid Compiler version 2.0");
}


method parseCommandLineArgs(args) {
    var abort = false;
    
    var keep       = false;
    var force      = false;
    var outputDir  = "";
    var tempDir    = "";
    var inputDir   = "";
    var plaidpath  = makeEmptyList();
    var inputFiles = makeEmptyList();
        
    while { !abort && !args.isEmpty() }{
        var handled = false;
        if ( args.car().equals("-h") || args.car().equals("--help") ) {
            handled = true;
            showUsage();
            abort = true
        };
        if ( args.car().equals("-v") || args.car().equals("--version") ) {
            handled = true;
            showVersion();
            abort = true
        };
        if ( args.car().equals("-d") || args.car().equals("--debug") ) {
            handled = true;
            args = args.cdr();
            ifElse ( args.car().isEmpty() || args.car().startsWith("-") ) {
                printLine("The '-d' option requires a parameter.");
                abort = true
            }{
                val debugValueString = args.car().trim();
                // cannot use valueOf because we cannot handle exceptions
                if ( debugValueString.equals("1") ) {
                    DEBUG_LEVEL = 1;
                };
                if ( debugValueString.equals("2") ) {
                    DEBUG_LEVEL = 2;
                };
                if ( debugValueString.equals("3") ) {
                    DEBUG_LEVEL = 3;
                };
                DEBUG(DEBUG_PRIO_MEDIUM, "Found debug option '" + args.car() + "'.");
            }
        };        
        if ( args.car().equals("-o") || args.car().equals("--output") ) {
            handled = true;
            args = args.cdr();
            ifElse ( args.car().isEmpty() || args.car().startsWith("-") ) {
                printLine("The '-o' option requires a parameter.");
                abort = true
            }{
                outputDir = args.car();
                DEBUG(DEBUG_PRIO_MEDIUM, "Found output directory option '" + args.car() + "'.");
            }
        };        
        if ( args.car().equals("-t") || args.car().equals("--temp") ) {
            handled = true;
            args = args.cdr();
            ifElse ( args.car().isEmpty() || args.car().startsWith("-") ) {
                printLine("The '-t' option requires a parameter.");
                abort = true
            }{
                tempDir = args.car();
                DEBUG(DEBUG_PRIO_MEDIUM, "Found temporary directory option '" + args.car() + "'.");
            }
        };
        if ( args.car().equals("-i") || args.car().equals("--input") ) {
            handled = true;
            args = args.cdr();
            ifElse ( args.car().isEmpty() || args.car().startsWith("-") ) {
                printLine("The '-i' option requires a parameter.");
                abort = true
            }{
                inputDir = args.car();
                DEBUG(DEBUG_PRIO_MEDIUM, "Found input directory option '" + args.car() + "'.");
            }
        };
        if ( args.car().equals("-p") || args.car().equals("--plaidpath") ) {
            handled = true;
            args = args.cdr();
            ifElse ( args.car().isEmpty() || args.car().startsWith("-") ) {
                printLine("The '-p' option requires a parameter.");
            }{
                val elems = classpathToList( args.car() );
                elems.map(fn (elem) => plaidpath.add(elem) );
                DEBUG(DEBUG_PRIO_MEDIUM, "Found plaidpath option '" + args.car()  + "'.");
            }
        };        
        if ( args.car().equals("-f") || args.car().equals("--force") ) {
            handled = true;
            force = true
        };        
        if ( args.car().equals("-k") || args.car().equals("--keep") ) {
            handled = true;
            force = true
        };    
        ifElse ( !handled ) {
            ifElse ( args.car().startsWith("-") ) {
                printLine("Unsupported option: " + args.car());
                abort = true
            }{
                // add remaining arguments as input files 
                ifElse ( inputDir.length() > 0 ) {
                    printLine("Cannot specify input directory '-i' and explicit files. Make your choice.");
                    abort = true
                }{
                    args.map( fn (x) => {
                        DEBUG(DEBUG_PRIO_MEDIUM, "Found input file  '" + args.car()  + "'.");
                        inputFiles.add(x)
                    });
                    args = makeEmptyList()
                }
            };
        }{        
            args = args.cdr()
        };
    };

    if ( inputDir == "" && inputFiles.isEmpty() ) {
        printLine("You must specify either an input directory('-i') or explicit file names.");
        abort = true
    };
   
    match (abort) {
        case True  { new None; }
        default    { 
            makeSome((new CompilerConfiguration {
                val plaidpath   = plaidpath;
                val force       = force;
                val keep        = keep;
                val outputDir   = outputDir;
                val inputDir    = inputDir;
                val tempDir     = tempDir;
                val inputFiles  = inputFiles;
            }).expand()); 
        }        
    }
}


/*****************************************************************************
 ** Job Management 
 ****************************************************************************/
val shared List jobs = makeEmptyList();

method void addJob(unique Job job) {
    jobs.add(job)
}

method void runJobsToParsingBarrierPass() {
    jobs.map( fn (job) => {
        var done = false;
        while { !job.passes.isEmpty() && !done  } {
            match (job.passes.car()) {
                case ParsingBarrierPass {
                    done = true;
                }
                default {
                    if ( job.status ) {
                        val pass = job.passes.car();
                        job.status =job.status && pass.run(job);
                    };
                }
           };
           // pop last pass of
           job.passes = job.passes.cdr();
        };
        job
    });
}

method void runJobsToNameExpanderBarrierPass() {
    jobs.map( fn (job) => {
        var done = false;
        while { !job.passes.isEmpty() && !done  } {
            match (job.passes.car()) {
                case NameExpanderBarrierPass {
                    done = true;
                }
                default {
                    if ( job.status ) {
                        val pass = job.passes.car();
                        job.status =job.status && pass.run(job);
                    };
                }
           };
           // pop last pass of
           job.passes = job.passes.cdr();
        };
        job
    });
}


method void runJobsToTypeResolutionBarrierPass() {
    jobs.map( fn (job) => {
        var done = false;
        while { !job.passes.isEmpty() && !done  } {
            match (job.passes.car()) {
                case TypeResolutionBarrierPass {
                    done = true;
                }
                default {
                    if ( job.status ) {
                        val pass = job.passes.car();
                        job.status =job.status && pass.run(job);
                    };
                }
           };
           // pop last pass of
           job.passes = job.passes.cdr();
        };
        job
    });
}


method void runJobsToCompletion() {
    jobs.map( fn (job) => {
        while { !job.passes.isEmpty() } {
            if ( job.status ) {
                val pass = job.passes.car();
                job.status =job.status && pass.run(job);
            };
            // pop last pass of
            job.passes = job.passes.cdr();
        };
        job
    });
}



method unique List getJobPasses() {
    val passes = makeEmptyList();

    // parsing and basic checks pass 
    passes.add(new ParsePass);
    passes.add(new FileSystemCheckPass);
    passes.add(new ImportCheckPass);
    passes.add(new ParsingBarrierPass);

    // name expanding passes
    passes.add(new NameExpanderPass);
    passes.add(new NameExpanderBarrierPass);

    // type resolution pass 
    passes.add(new TypeResolutionPass);
    passes.add(new TypeResolutionBarrierPass);

    // report errors pass 
    passes.add(new ReporterPass);

    passes
}


method main(args) {

    val unique Option result = parseCommandLineArgs(args);
   
    match (result) {
        case Some {
            val unique CompilerConfiguration cc = result.value;
            val sourceJobs = makeEmptyList();
            val shared Resolver resolver = new ClasspathResolver { classpath = cc.plaidpath; };

            cc.inputFiles.map(fn (path) => {
                val source = new Source { val path = path; };
                val job = new UserJob { 
                    val source = source; 
                    val resolver = resolver;
                    val reporter = new reporter.ConsoleReporter;
                    var passes = getJobPasses();
                };
                
                // add to source jobs
                addJob(job);
            });                                        

            // parse jobs
            runJobsToParsingBarrierPass();
            // expand names
            runJobsToNameExpanderBarrierPass();
                        
            // run remaining passes 
            runJobsToCompletion();
        }
        default {
            printLine("Could not find any source files.");
        }
    }
}