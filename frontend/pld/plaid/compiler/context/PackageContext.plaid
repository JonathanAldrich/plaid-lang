package plaid.compiler.context;

import java.util.HashMap;

import plaid.compiler.Context;
import plaid.compiler.resolver.ClassPathResolver;
import plaid.compiler.Resolver;

import plaid.compiler.DEBUG2;
import plaid.compiler.DEBUG3;


state PackageContext case of Context {

    val unique Map cache = HashMap.new();
    val shared Context parent;
    val shared ClasspathResolver resolver;
    val immutable String packageName;
    
    method put(name, symbol) {
        false
    }

    method find(immutable String name) {        
        var unique Option symbol = NONE;                                
        DEBUG2("<PackageContext> looking for '" + name + "'");        

        ifElse ( cache.containsKey(name) ) {
            DEBUG3("<PackageContext> found '" + name +"' in cache");
            symbol = cache.get(name);
        }{
            DEBUG3("<PackageContext>     looking for '" + packageName + "."+ name +"'");
            symbol  = resolver.lookup(packageName + "." + name);
        };
        cache.put(name, symbol);

        match (symbol) {
            case Some { symbol }
            case None { parent.find(name) }
        }
    }
}