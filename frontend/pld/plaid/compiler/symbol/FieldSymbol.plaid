package plaid.compiler.symbol;

import plaid.compiler.*;
import plaid.ast.parsed.*;
import plaid.compiler.util.makeTypeFromAST;

state FieldSymbol case of Symbol {
    val immutable ASTNode declaration;
    val immutable Symbol owner;
    var immutable Option<immutable FieldType> fieldType = NONE;

    method immutable FieldType getType() {
        match (this.fieldType) {
            case Some {
                this.fieldType.value;
            }
            case None {
                DEBUG(DEBUG_PRIO_LOW, "<FieldSymbol> compute type for field");
                val fieldType = makeTypeFromAST(this.declaration.fieldType);
                var isVar     = false;        

                match (this.declaration.specifier) {
                    case VarSpecifier {
                        isVar = true;
                    }
                    default {
                        isVar = false;
                    }
                };

                this.fieldType = makeSome(new FieldPlaidType {
                    val isVar    = isVar;
                    val fileType = fieldType;
                    val name     = this.declaration.name.name;
                });
                this.fieldType.value
            }
        }
    }
}