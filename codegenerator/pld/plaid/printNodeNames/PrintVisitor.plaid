package plaid.printNodeNames;

import plaid.ast.*;
import plaid.ast.visitor.ASTVisitor;
import plaid.metadataCreation.Node;
import plaid.metadataCreation.Tree;
import plaid.printNodeNames.StateTableEntry;

state PrintVisitor = ASTVisitor {
	var debug=false;
	var newVariableNameNumber = 0;
	var states = new plaid.lang.List { var head = new plaid.lang.Nil;};
	var firstCase = false;
	var currState = "";
	var browser=true;
	var mainFile = true;
	var targetDirectory = "../../compiledJavaScript";
	var output = new plaid.io.FileObj;
	
	method getState(name){
		var returnVal="";
		this.states.map(fn (a) => {
			if(a.name==name){
				returnVal=a;
			}
		});
		//if return is never given a value, this is an error (trying to be case of a state that has not been declared)
		
		//copies the tree to which name belongs
		var returnTree=returnVal.tree.copyTree(returnVal.node);
		//creates a new state table entry with the name passed in, with a copy of that name's node, with a copy of that name's tree
		var newEntry = new StateTableEntry with { name = returnVal.name; node = returnTree.targetNode; tree = returnTree; };
		newEntry;
	}
	
	method generateNewVariableName() {
		this.newVariableNameNumber = this.newVariableNameNumber+1;
		"plaidNewVar"+this.newVariableNameNumber;
	}
	
	method generateNewStateName(str) {
		"plaidNewState_"+str;
	}

/*	
	method checkUniqueMembers(root1,root2){
		var originalMembers = root1.members();
		var withMembers = root2.members();
		checkUniqueMembersHelper(originalMembers,withMembers);
	}
	method checkUniqueMembersHelper(originalMembers,withMembers){
		
		
		output.writeLine("-----------------------------------");
		originalMembers.map(fn (c) => {
			output.writeLine(c);
		});	
		withMembers.map(fn (c) => {
			output.writeLine(c);
		});	
		output.writeLine("-----------------------------------");
		
		
		originalMembers.map(fn (c) => {
			withMembers.map(fn (d) => {
				if (c==d){
					output.writeLine("Error: Attempt to compose state from two states containing member with same name"+c);
				};
			});
		});		
	}
*/
	
	method checkUniqueTags(root1,root2){
		var originalTags = root1.tags();
		var withTags = root2.tags();
		checkUniqueTagsHelper(originalTags,withTags);
	}
	method checkUniqueTagsHelper(originalTags,withTags){
		
		/*
		output.writeLine("-----------------------------------");
		originalTags.map(fn (c) => {
			output.writeLine(c);
		});	
		withTags.map(fn (c) => {
			output.writeLine(c);
		});	
		output.writeLine("-----------------------------------");
		*/
		
		originalTags.map(fn (c) => {
			withTags.map(fn (d) => {
				if (c==d && !(c=="")){
					output.writeLine("Error: Attempt to compose state from two states containing tag with same name"+c);
				};
			});
		});	
	}
		
	method specializeState(allMembers,withMembers,memberToAdd,memberValue,withEntry,targetStateName){
		var foundMatch=false;
		withMembers.map(fn (c) => {
			if (c==memberToAdd){
				foundMatch=true;
				output.writeLine(targetStateName+"['"+memberToAdd+"'] = '"+memberValue+"';");
			};
		});	
		if (foundMatch==false) {
			allMembers.map(fn (c) => {
				if (c==memberToAdd){
				output.writeLine("Error: Attempt to compose state from two states containing member with same name");
				};
			});	
			output.writeLine(generateNewStateName(targetStateName)+"['"+memberToAdd+"'] = "+memberValue+";");
			//must add the member to the tree now
			withEntry.node.memberList.add("'"+memberToAdd+"'");
		};
	}
	
	method handleAddWithState(qiToAdd,parentNode,parentTree){
		match (qiToAdd) {	
			case QI {			
				//the string name of the state that is a with state for the current state
				var withState = qiToAdd.qid.toString();	
				//the object that bundles the node and tree corresponding to the state name above
				var withEntry = this.getState(withState);
				var withRoot = withEntry.tree.root;
				var tagStateName = generateNewStateName(parentNode.tag);
				var withTagStateName = generateNewStateName(withEntry.name);
				
				//ensure the members being added by the with state are not already present in the state to which they are added
				//checkUniqueMembers(parentTree.root,withRoot);
		
				//ensure the tags being added by the with state are not already present in the state to which they are added
				checkUniqueTags(parentTree.root,withRoot);
		
				withRoot.children.map(fn (c) => {
					parentNode.children.add(c);
				});
				
				var withMembersList = withRoot.members();
				withMembersList.map(fn (d) => {
					output.writeLine(tagStateName+"["+d+"] = "+withTagStateName+"["+d+"];");
				});
				
				match (qiToAdd.initState) {
					case Some{
						match (qiToAdd.initState.value) {
							case DeclList{
								var withMembers = withEntry.node.members();
								var allMembers = withRoot.members();
								qiToAdd.initState.value.decls.map(fn (d) => {
									var funcName = "";
									match (d){
										case FieldDecl{
											funcName=d.f.name;
										}
										case MethodDecl{
											funcName=d.name;
										}
										default{
											output.writeLine("problem in specialize");
										}
									};
									var funcValue = d.accept(this);
									//check unique members
									specializeState(allMembers, withMembers, funcName, funcValue, withEntry, parentNode.tag);
								});
							}
							default{
								output.writeLine("//problem in QI in StateDecl in handleAddWith");
							}
						};
					}
					case None{
					}
				};
				
			}
			
			default{
				output.writeLine("problem");
			}
		};
		
	}

	method visitApplication(node) {
		if (debug) {
			output.writeLine("//I am an Application");
			output.writeLine ("   //f: " + node.f.toString());
			output.writeLine ("   //arguments: " + node.arguments.toString());
			output.writeLine ("   //isTailCall: " + node.isTailCall.toString());
		};
		
		//make a string (with argument values) from list of argument objects	
		var argumentsString="";
		if (node.arguments.length() > 0) {
			var nMinusOneArguments = node.arguments.subList(0,node.arguments.length() - 2);
			nMinusOneArguments.map(fn (a) => argumentsString=argumentsString+a.accept(this)+",");
			argumentsString=argumentsString+(node.arguments.get(node.arguments.length() - 1).accept(this));
		};
		
		
		//is there a problem here?  where a name cannot be found?
		var mn = node.f.name;
		
		//------------------------------------------------------------
		//this is temporary while we figure out packages.  fix this later!
		var extraArgumentToRemoveLater = "";	
		if (mn=="printLine"){
			extraArgumentToRemoveLater = "+\"<BR>\"";
		};
		if (mn=="printLine" || mn=="print"){
			mn="document.write";
		};	
		//---------------------------------------------------------------
		
		//immediately emit code that assigns method call's value to the newVarName generated below
		
		var newVarName = generateNewVariableName();
		var funcCallString = "var "+newVarName+" = "+mn+"("+argumentsString+extraArgumentToRemoveLater+");";
		//print the function call
		output.writeLine (funcCallString);
		//return the newVarName so that method call's value can be used by the method that called this one (this can be used just like an ID name)
		newVarName;
	}
	method visitAssignment(node) {
		if (debug) {
			output.writeLine("//I am an Assignment");
			output.writeLine ("   //target: " + node.target.toString());
			output.writeLine ("   //field: " + node.field.toString());
			output.writeLine ("   //value: " + node.value.toString());
		};
		var fieldName = node.field.accept(this);
		var fullName = "";
		match (node.target) {
			case None{
				fullName = fieldName;
			}
			case Some{
				fullName = node.target.value.accept(this)+"."+fieldName;
			}
		};
		output.writeLine (fullName+" = "+node.value.accept(this)+";");
		//node.target.value.accept(this); 
		fullName;
	}
	method visitAtomicBlock(node) {
		if (debug) {
			output.writeLine("//I am an AtomicBlock");
		};
	}
	method visitBorrowBinding(node) {
		if (debug) {
			output.writeLine("//I am a BorrowBinding");
		};
		node.exp.accept(this); 
		node.body.accept(this); 
	}
	method visitCase(node) {
		if (debug) {
			output.writeLine("//I am a Case");
			output.writeLine ("   //qi: " + node.qi.toString());
			output.writeLine ("   //x: " + node.x.toString());
			output.writeLine ("   //e: " + node.e.toString());
			output.writeLine ("   //defaultCase: " + node.defaultCase.toString());
			output.writeLine ("   //boundVar: " + node.boundVar.toString());
		};
		//the one called below should only be called if this case is not the default case
		
		ifElse(this.firstCase){
			output.writeLine("if (matchVar.match(\""+node.qi.value.qid.toString()+"\")) {"); 
			this.firstCase=false;	
		}
		{
			ifElse(node.defaultCase){
				output.writeLine("else {");
			}
			{
				output.writeLine("else if (matchVar.match(\""+node.qi.value.qid.toString()+"\")) {"); 
			};
		};
		node.e.accept(this); 
		output.writeLine("}");
		"matchVar";
	}
	method visitCast(node) {
		if (debug) {
			output.writeLine("//I am a Cast");
		};
		node.body.accept(this);
	}
	method visitChangeState(node) {
		if (debug) {
			output.writeLine("//I am a ChangeState");
			output.writeLine ("   //e: " + node.e.toString());
			output.writeLine ("   //s: " + node.s.toString());
		};
		var toChange=node.e.accept(this);
		
		match(node.s){
			case QI{
				output.writeLine(toChange+".stateChange("+node.s.accept(this)+");");
			}
			case With{
				output.writeLine(toChange+".stateChange("+node.s.accept(this)+");");
			}
			case DeclList{
				node.s.decls.map(fn (d) => {
					match(d) {
						case MethodDecl{
							//java.lang.System.out.print(toChange+"['"+d.name+"'] = "); 
							//d.accept(this);
							output.writeLine(toChange+".stateChange("+toChange+".withMember('"+d.name+"', '"+d.accept(this)+"'));");
							}
						case FieldDecl{			
							match(d.e){
								case UnitLiteral{
									//output.writeLine(toChange+".stateChange("+toChange+".withMemberNoValue('"+d.f.name+"'));");
									output.writeLine(toChange+".stateChangeMemberNoValue('"+d.f.name+"');");
								}
								default {
									//output.writeLine(toChange+".stateChange("+toChange+".withMember('"+d.f.name+"', '"+d.e.accept(this)+"'));");
									output.writeLine(toChange+".stateChangeMember('"+d.f.name+"', '"+d.e.accept(this)+"');");
								}
							};
						
						}
						default{
							d.accept(this);
						}
					};
				});
			}
			default{
				output.writeLine("problem at changeState's s");
				node.s.accept(this);
			}
		};
		toChange;
	}
	method visitCompilationUnit(node) {
		if (debug) {
			output.writeLine("//I am a CompilationUnit");	
		};
		
		var directoryFile = new plaid.io.FileObj;
		directoryFile.setName(targetDirectory);
		var modifiedPackage="";
		
		ifElse(directoryFile.canWrite()){
			node.packageName.map(fn (d) => {
				modifiedPackage=modifiedPackage+"/"+d;
			});
			directoryFile.setName(targetDirectory+modifiedPackage);
			directoryFile.mkdirs();
		}
		{
			output.writeLine("Error: cannot write to the file "+targetDirectory+modifiedPackage);
		};
		
		var fileName=node.sourceFile;
		java.lang.System.out.println(fileName);	
		var fileExtension=".js";
		if(browser==true && mainFile==true){
			fileExtension=".html";
		};
		output.setName(targetDirectory+modifiedPackage+"/"+fileName+fileExtension);
		output.beginWrite();
		
		ifElse (browser==true) {
		    if(mainFile==true){
				output.writeLine("<html>");
				output.writeLine("<body>");
				output.writeLine("<script src=\"prototype.js\" type=\"text/javascript\"></script>");
				output.writeLine("<script src=\"Tree.js\" type=\"text/javascript\"></script>");
				output.writeLine("<script src=\"PlaidObject.js\" type=\"text/javascript\"></script>");
				output.writeLine("<script src=\"PlaidState.js\" type=\"text/javascript\"></script>");
				output.writeLine("<script type=\"text/javascript\">");
				output.writeLine("<!--");
			
				output.writeLine("var Script = {");
				output.writeLine("loadedScripts: [],");
				output.writeLine("has: function(array, item) {");
				output.writeLine("var length=array.length;");
				output.writeLine("for (var i=0;i<length;i++){");
				output.writeLine("if (array[i]===item){");
				output.writeLine("return true;");
				output.writeLine("}");
				output.writeLine("}");
				output.writeLine("return false;");
				output.writeLine("},");
				output.writeLine("include: function(script){");
				output.writeLine("if (Script.has(this.loadedScripts,script)){");
				output.writeLine("return false;");
				output.writeLine("}");
				output.writeLine("var code = new Ajax.Request(script, {");
				output.writeLine("asynchronous: false, method: \"GET\",");
				output.writeLine("evalJS: false, evalJSON: false");
				output.writeLine("}).transport.responseText;");
				output.writeLine("if (Prototype.Browser.IE) {");
				output.writeLine("window.execScript(code);");
				output.writeLine("} else if (Prototype.Browser.WebKit){");
				output.writeLine("$$(\"head\").first().insert(Object.extend(");
				output.writeLine("new Element(\"script\", {type: \"text/javascript\"}), {text: code}");
				output.writeLine("));");
				output.writeLine("} else {");
				output.writeLine("window.eval((code));");
				output.writeLine("}");
				output.writeLine("this.loadedScripts.push(script);");
				output.writeLine("}");
				output.writeLine("};");
			};
		}
		{
			output.writeLine("load(\"PlaidObject.js\");");
			output.writeLine("load(\"PlaidState.js\");");
			output.writeLine("load(\"Tree.js\");");
		};
		node.imports.accept(this);
		node.decls.map(fn (d) => {
			d.accept(this);
		});
		if (browser==true && mainFile==true){
			output.writeLine("//-->");
			output.writeLine("</script>");
			output.writeLine("</body>");
			output.writeLine("</html>");
		};
		output.endWrite();
	}
	method visitDeclList(node) {
		if (debug) {
			output.writeLine("//I am a DeclList");
		};
		//node.decls.map(fn (d) => {d.accept(this);});
		
		var nMinusOneDecls = node.decls.subList(0,node.decls.length() - 2);
		nMinusOneDecls.map(fn (d) => {d.accept(this);});
		node.decls.get(node.decls.length() - 1).accept(this);
	}
	method visitDereference(node) {
		if (debug) {
			output.writeLine("//I am a Dereference");
			output.writeLine ("   //left: " + node.left.toString());
			output.writeLine ("   //right: " + node.right.toString());
		};
		node.left.accept(this)+"."+node.right.accept(this); 
	}
	method visitDestructiveDereference(node) {
		if (debug) {
			output.writeLine("//I am a DestructiveDereference");
		};
	}
	method visitDoubleLiteral(node) {
		if (debug) {
			output.writeLine("//I am a DoubleLiteral");
			output.writeLine ("   //double: " + node.double);
		};
		node.double;
	}
	method visitFieldDecl(node) {
		if (debug) {
			output.writeLine("//I am a FieldDecl");
			output.writeLine ("   //f: " + node.f.toString());
			output.writeLine ("   //e: " + node.e.toString());
			output.writeLine ("   //abstractField: " + node.abstractField.toString());
			output.writeLine ("   //overrides: " + node.overrides.toString());
			output.writeLine ("   //fieldType: " + node.fieldType.toString());
		};
		match(node.e){
			case UnitLiteral{
				output.writeLine("var "+node.f.name+";");
			}
			default {
				output.writeLine("var "+node.f.name+" = "+node.e.accept(this)+";");
			}
		};
		node.f.name;
	}
	method visitGroupDecl(node) {
		if (debug) {
			output.writeLine("//I am a GroupDecl");
		};
	}
	method visitGroupParameter(node) {
		if (debug) {
			output.writeLine("//I am a GroupParameter");
		};
	}
	method visitID(node) {
		if (debug) {
			output.writeLine("//I am an ID");
			output.writeLine ("   //name: " + node.name);
		};		
		var returnName=node.name;
		if (returnName=="this$plaid"){
			returnName="this";
		};
		returnName;
	}
	method visitImportList(node) {
		if (debug) {
			output.writeLine("//I am an ImportList");
			output.writeLine("   //token: "+node.token.toString());
		};
		ifElse(browser==true) {
			node.imports.map(fn (d) => {
				output.writeLine("Script.include(\""+d.qid.toString()+".js\");");
			});
		}
		{
			node.imports.map(fn (d) => {
				output.writeLine("load(\""+d.qid.toString()+".js\");");
			});
		};
	}
	method visitIntLiteral(node) {
		if (debug) {
			output.writeLine("//I am an IntLiteral");
			output.writeLine ("   //integer: " + node.integer);
		};
		node.integer;
	}
	method visitJoin(node) {
		if (debug) {
			output.writeLine("//I am a Join");
		};
		node.body.accept(this);
	}
	method visitLambda(node) {
	
		if (debug) {
			output.writeLine("//I am a Lambda");
			output.writeLine ("   //arguments: " + node.arguments.toString());
			output.writeLine ("   //body: " + node.body.toString());
			output.writeLine ("   //lambdaType: " + node.lambdaType.toString());
			output.writeLine ("   //recursiveName: " + node.recursiveName.toString());
		};
		//make a strings (with argument values) from list of argument objects	
		var newVarName = generateNewVariableName();
		var argumentsString="";
		if (node.arguments.length() > 0) {
			var nMinusOneArguments = node.arguments.subList(0,node.arguments.length() - 2);
			nMinusOneArguments.map(fn (a) => argumentsString=argumentsString+a.accept(this)+",");
			argumentsString=argumentsString+(node.arguments.get(node.arguments.length() - 1).accept(this));
		};
		output.writeLine ("var "+newVarName+" = function("+argumentsString+") {");
		output.writeLine ("return "+node.body.accept(this)+";"); 
		output.writeLine ("};");
		newVarName;
	}
	method visitLetBinding(node) {
		if (debug) {
			output.writeLine("//I am a LetBinding");
			output.writeLine ("   //x: " + node.x.toString());
			output.writeLine ("   //exp: " + node.exp.toString());
			output.writeLine ("   //body: " + node.body.toString());
			output.writeLine ("   //mutable: " + node.mutable.toString());
		};
		output.writeLine("var "+node.x.accept(this)+" = "+node.exp.accept(this)+";"); 
		node.body.accept(this); 
	}
	method visitMatch(node) {
		if (debug) {
			output.writeLine("//I am a Match");
			output.writeLine ("   //e: " + node.e.toString());
			output.writeLine ("   //caseList: " + node.caseList.toString());
		};
		output.writeLine("var matchVar = "+node.e.accept(this)+";");
		this.firstCase = true;
		node.caseList.map(fn (c) => {
			c.accept(this);
		});
		node.e.accept(this);
	}
	method visitMetaDereference(node) {
		if (debug) {
			output.writeLine("//I am a MetaDereference");
		};
	}
	method visitMetaTypeArgument(node) {
		if (debug) {
			output.writeLine("//I am a MetaTypeArgument");
		};
	}
	method visitMetaVariable(node) {
		if (debug) {
			output.writeLine("//I am a MetaVariable");
		};
	}
	method visitMethodCall(node) {
		if (debug) {
			output.writeLine("//I am a MethodCall");
			output.writeLine ("   //receiver: " + node.receiver.toString());
			output.writeLine ("   //methodName: " + node.methodName.toString());
			output.writeLine ("   //arguments: " + node.arguments.toString());
			output.writeLine ("   //isTailCall: " + node.isTailCall.toString());
		};
		//make a string (with argument values) from list of argument objects	
		var argumentsString="";
		if (node.arguments.length() > 0) {
			var nMinusOneArguments = node.arguments.subList(0,node.arguments.length() - 2);
			nMinusOneArguments.map(fn (a) => argumentsString=argumentsString+a.accept(this)+",");
			argumentsString=argumentsString+(node.arguments.get(node.arguments.length() - 1).accept(this));
		};
		//immediately emit code that assigns method call's value to the newVarName generated below
		var newVarName = generateNewVariableName();
		var mn = node.methodName.name;
		var funcCallString = "";
		
		ifElse ( mn=="+" || mn=="-" || mn=="*" ||mn=="/" ){
			funcCallString = "var "+newVarName+" = "+node.receiver.accept(this)+" "+mn+" "+argumentsString+";";
		}
		{
			funcCallString = "var "+newVarName+" = "+node.receiver.accept(this)+"."+mn+"("+argumentsString+");";
		};
		//print the function call
		output.writeLine (funcCallString);
		node.receiver.accept(this);
		//return the newVarName so that method call's value can be used by the method that called this one (this can be used just like an ID name)
		newVarName;
		
	}
	method visitMethodDecl(node) {
		if (debug) {
			output.writeLine("//I am a MethodDecl");
			output.writeLine ("   //name: " + node.name);
			output.writeLine ("   //body: " + node.body.toString());
			output.writeLine ("   //arguments: " + node.arguments.toString());
			output.writeLine ("   //abstractMethod: " + node.abstractMethod.toString());
			output.writeLine ("   //overrrides: " + node.overrides.toString());
			output.writeLine ("   //methodType: " + node.methodType.toString());
		};
		
		//make a strings (with argument values) from list of argument objects	
		var argumentsString="";
		if (node.arguments.length() > 0) {
			var nMinusOneArguments = node.arguments.subList(0,node.arguments.length() - 2);
			nMinusOneArguments.map(fn (a) => argumentsString=argumentsString+a.accept(this)+",");
			argumentsString=argumentsString+(node.arguments.get(node.arguments.length() - 1).accept(this));
		};
		output.writeLine ("function "+node.name+" ("+argumentsString+") {");
		output.writeLine ("return "+node.body.accept(this)+";"); 
		output.writeLine ("}");
		if(node.name=="main"){
			output.writeLine("main();");
		};
		node.name;
	}	
	method visitNewInstance(node) {
		if (debug) {
			output.writeLine("//I am a NewInstance");
			output.writeLine ("   //s: " + node.s.toString());
		};
		var newVarName = generateNewVariableName();
		match(node.s){
			case QI{
				output.writeLine("var "+newVarName+" = "+(node.s.accept(this))+".instantiate();");
			}
			case With{
				output.writeLine("var "+newVarName+" = ("+node.s.accept(this)+").instantiate();");
			}			
			case DeclList{
				var tagName = generateNewVariableName();
				var returnString = "(new PlaidState())";
				node.s.decls.map(fn (d) => {
					var newMember = d.accept(this);
					returnString=returnString+".withMember('"+newMember+"', "+newMember+")";
				});
				output.writeLine("var "+newVarName+" = ("+returnString+").instantiate();");
			}
		};
		newVarName;
	}
	method visitQI(node) {
		if (debug) {
			output.writeLine("//I am a QI");
			output.writeLine ("   //qid: " + node.qid.toString());
			output.writeLine ("   //initState: " + node.initState.toString());
		};
		var returnString = node.qid.toString();
		match (node.initState) {
			case Some{
				//node.initState.value.accept(this);
				match (node.initState.value) {
					case DeclList{
						var tagName = returnString;
						returnString=generateNewStateName(tagName);
						node.initState.value.decls.map(fn (d) => {
							var newMember = d.accept(this);
							returnString=returnString+".specialize('"+tagName+"', '"+newMember+"', "+newMember+")";
						});
					}
					default{
						output.writeLine("//problem in visitQI - initState exists but is not DeclList");
					}
				};
			}
			case None{
				returnString = generateNewStateName(returnString);
			}
			default{
			}
		};
		//if node.initState is Some
		//node.initState.value.accept(this);
		//if node.initState is DeclList
		//node.initState.accept(this); 
		returnString;
	}
	method visitSplitBlock(node) {
		if (debug) {
			output.writeLine("//I am a SplitBlock");
		};
	}
	method visitStateDecl(node) {
			if (debug) {
			output.writeLine("//I am a StateDecl");	
			output.writeLine ("   //name: " + node.name.toString());
			output.writeLine ("   //metaParams: " + node.metaParams.toString());
			output.writeLine ("   //caseOf: " + node.caseOf.toString());
			output.writeLine ("   //isCaseOf: " + node.isCaseOf.toString());
			output.writeLine ("   //stateType: " + node.stateType.toString());
			output.writeLine ("   //metaCaseOfArgs: " + node.metaCaseOfArgs.toString());
			output.writeLine ("   //stateDef can be DeclList Freeze QI With");
			output.writeLine ("   //stateDef can be all kinds of things");
		};
		var currStateName = generateNewStateName(node.name.name);
		currState = currStateName;
		//emit code for creating a new Plaid state
		output.writeLine("var "+currStateName+" = new PlaidState();");
		
		var tree = "";
		var treeNode = "";
		match(node.caseOf) { 
			case Some { 
				//the state is a case of some other state, stored in caseOf.value.toString()
				//create metadata string for creating Plaid state object in Javascript runtime
				
				//the string name of the state of which the current state is a case
				var caseOfState = node.caseOf.value.qid.toString();
				//the object that bundles the node and tree corresponding to the state name above
				var caseOfEntry = this.getState(caseOfState);
				//the tree to which the current state will be added
				var caseOfEntryTree = caseOfEntry.tree;
				//the node in the above tree that corresponds to the state of which the current state is a case
				var caseOfEntryNode = caseOfEntry.node;
				//the node that will reflect the current state
				treeNode = new Node;
				var memberList = new plaid.lang.List { var head = new plaid.lang.Nil;};
				
				//add members to the previously empty list
				match(node.stateDef){
					case DeclList{
						node.stateDef.decls.map(fn (d) => {
							match(d) {
								case MethodDecl {
									memberList.add("'"+d.name+"'");
								}
								case FieldDecl {		
									match(d.e){
										case UnitLiteral{
										}
										default {
											memberList.add("'"+d.f.name+"'");
										}
									};
								}
								default {
									output.writeLine("problem at StateDecl's DeclList, one of the decls");
								}
							};
						});
					}
					default{
						//no methods or fields
					}
				};
				
				//create a list with the tag of the new state, to check unique tags
				var nameList = new plaid.lang.List { var head = new plaid.lang.Nil;};
				nameList.add(node.name.name);
				
				//ensure the members being added by the current state are not already present in the state to which they are added
				//checkUniqueMembersHelper(caseOfEntryTree.root.members(),memberList);
				//ensure the tag being added by the current state is not already present in the state to which it is added
				checkUniqueTagsHelper(caseOfEntryTree.root.tags(),nameList);
				
				//add the appropriate members to the new state
				var tagSN = generateNewStateName(node.name.name);
				var caseOfTagStateName = generateNewStateName(caseOfEntryNode.tag);
				var caseOfMembersList = caseOfEntry.tree.root.members();
				caseOfMembersList.map(fn (d) => {
					output.writeLine(tagSN+"["+d+"] = "+caseOfTagStateName+"["+d+"];");
				});
				
				treeNode.setData(node.name.name, memberList, false, caseOfEntryNode);
				tree=caseOfEntryTree;
			} 
			case None { 
				//the state is not a case of any other state
				//create metadata string for creating Plaid state object in Javascript runtime
				tree=new Tree;
				treeNode = new Node;
				var memberList = new plaid.lang.List { var head = new plaid.lang.Nil;};
				
				//add members to the previously empty list
				match(node.stateDef){
					case DeclList{
						node.stateDef.decls.map(fn (d) => {
							match(d) {
								case MethodDecl {
									memberList.add("'"+d.name+"'");
								}
								case FieldDecl {		
									match(d.e){
										case UnitLiteral{
										}
										default {
											memberList.add("'"+d.f.name+"'");
										}
									};
								}
								default {
									output.writeLine("problem at StateDecl's DeclList, one of the decls");
								}
							};
						});
					}
					default{
						//no methods or fields
					}
				};
				treeNode.setData(node.name.name, memberList, true, tree.root);
			} 
		};
		
		match(node.stateDef){
			case DeclList{
				node.stateDef.decls.map(fn (d) => {
					match(d) {
						case MethodDecl{
							output.write(currStateName+"['"+d.name+"'] = "); 
							d.accept(this);
						}
						case FieldDecl{			
							match(d.e){
								case UnitLiteral{
								}
								default {
									output.writeLine(currStateName+"['"+d.f.name+"'] = "+d.e.accept(this)+";");
								}
							};
						}
						default{
							output.writeLine("//StateDecl's DeclList has something that's not MethodDecl or FieldDecl");
							d.accept(this);
						}
					};
				});
			}
			case QI{
				//the state in this QI should be a component of the state being added (a with state for it)
				handleAddWithState(node.stateDef,treeNode,tree);
			}
			case With{
				handleWithStateDecl(node.stateDef.s1,treeNode,tree,currStateName);
				handleWithStateDecl(node.stateDef.s2,treeNode,tree,currStateName);
			}
			default{
				output.writeLine("//problem in StateDecl's stateDef, not QI, With, or DeclList");
				node.stateDef.accept(this);
			}
		};
		var entry=new StateTableEntry with { name = node.name.name; node = treeNode; tree = tree; };
		this.states.add(entry);
		var md = tree.toMetadata(tree.root);
		//emit code for setting the tree of the new Plaid state
		output.writeLine(currStateName+".tree = "+md+";");
		
		//node.caseOf.value.accept(this);
        currStateName;
	}
	
	method handleWithStateDecl(stateDesc,treeNode,tree,currStateName){
		match (stateDesc) {
			case QI {
				//the QI s1 in this With should be a component of the state being added (a with state for it)
				handleAddWithState(stateDesc,treeNode,tree);
			}
			case DeclList{
				var memberList2 = new plaid.lang.List { var head = new plaid.lang.Nil;};
				stateDesc.decls.map(fn (d) => {
					match(d) {
						case MethodDecl{
							memberList2.add("'"+d.name+"'");
							output.write(currStateName+"['"+d.name+"'] = "); 
							d.accept(this);
						}
						case FieldDecl{			
							match(d.e){
								case UnitLiteral{
								}
								default {
									memberList2.add("'"+d.f.name+"'");
									output.writeLine(currStateName+"['"+d.f.name+"'] = "+d.e.accept(this)+";");
								}
							};
						}
						default{
							output.writeLine("//StateDecl's DeclList has something that's not MethodDecl or FieldDecl");
							d.accept(this);
						}
					};
				});	
				//ensure the members being added by the current state are not already present in the state to which they are added
				//checkUniqueMembersHelper(tree.root.members(),memberList2);
				var treeNode2 = new Node;
				treeNode2.setData("", memberList2, true, treeNode);
			}
			case With {
				handleWithStateDecl(stateDesc.s1,treeNode,tree,currStateName);
				handleWithStateDecl(stateDesc.s2,treeNode,tree,currStateName);
			}
			default{
			    output.writeLine("//possible problem at With in stateDecl");
				output.writeLine(node.stateDef.s2.accept(this));
			}
		};
	}
	
	method visitStringLiteral(node) {
		if (debug) {
			output.writeLine("//I am a StringLiteral");
			output.writeLine ("   //string: " + node.string);
		};
		("\"")+node.string+("\"");
	}
	method visitsplitPerm(node) {
		if (debug) {
			output.writeLine("//I am a Split");
		};
		node.body.accept(this);
	}
	method visitTakeBinding(node) {
		if (debug) {
			output.writeLine("//I am a TakeBinding");
		};
		node.exp.accept(this); 
		node.body.accept(this); 
	}
	method visitTypeParameter(node) {
		if (debug) {
			output.writeLine("//I am a TypeParameter");
		};
	}
	method visitUnitLiteral(node) {
		if (debug) {
			output.writeLine("//I am a UnitLiteral");
		};
		"undefined";
	}
	method visitUnpackerInnerGroups(node) {
		if (debug) {
			output.writeLine("//I am an UnpackerInnerGroups");
		};
	}
	method visitWith(node) {
		if (debug) {
			output.writeLine("//I am a With");
			output.writeLine ("   //s1: " + node.s1.toString());			
			output.writeLine ("   //s2: " + node.s2.toString());
		};
		var returnString="";
		var tagName="";
		match (node.s1) {
			case QI{
				tagName = node.s1.qid.toString();
				returnString=node.s1.accept(this);
			}
			case DeclList{
				tagName = generateNewVariableName();
				//output.writeLine("var plaidNewState_"+tagName+" = new PlaidState();");
				//returnString = generateNewStateName(tagName);
				returnString = "(new PlaidState())";
				node.s1.decls.map(fn (d) => {
					var newMember = d.accept(this);
					returnString=returnString+".withMember('"+newMember+"', "+newMember+")";
				});
			}
			default{
				output.writeLine("problem at With");
			}
		};
		match (node.s2) {
			case QI{
				returnString = returnString+".with("+node.s2.accept(this)+")"; 
			}
			case DeclList{
				node.s2.decls.map(fn (d) => {
					var newMember = d.accept(this);
					//returnString=returnString+".specialize('"+tagName+"', '"+newMember+"', "+newMember+")";
					returnString=returnString+".withMember('"+newMember+"', "+newMember+")";
				});
			}
			case With{
				returnString = returnString+withHelper(node.s2);
			}
			default{
				output.writeLine("problem at With");
				node.s2.accept(this);
			}
		};
		returnString;
	}
	method withHelper(node){
		var returnString="";
		match (node.s1) {
			case QI{
				returnString = ".with("+node.s1.accept(this)+")"; 
			}
			case DeclList{
				node.s1.decls.map(fn (d) => {
					var newMember = d.accept(this);
					returnString=returnString+".withMember('"+newMember+"', "+newMember+")";
				});
			}
			default{
				output.writeLine("problem at With");
				node.s1.accept(this);
			}
		};
		match (node.s2) {
			case QI{
				returnString = returnString+".with("+node.s2.accept(this)+")"; 
			}
			case DeclList{
				node.s2.decls.map(fn (d) => {
					var newMember = d.accept(this);
					returnString=returnString+".withMember('"+newMember+"', "+newMember+")";
				});
			}
			case With{
				returnString = returnString+this.withHelper(node.s2);
			}
			default{
				output.writeLine("problem at With");
				node.s2.accept(this);
			}
		};
		returnString;
	}

}